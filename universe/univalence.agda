{-

The universe is univalent, proven using Glue types. We follow Cohen, Coquand, Huber, and
MÃ¶rtberg (https://doi.org/10.4230/LIPIcs.TYPES.2015.5, Â§7.2), who prove univalence in the
form

(A : ğ‘¼) â†’ IsContr (Î£ B : ğ‘¼. El B â‰ƒ El A).

The addition of an equivariance condition on fibrations has no effect on this proof.

-}
open import prelude
open import axiom
open import internal-extensional-type-theory
open import cofibration
open import fibration.fibration
open import fibration.trivial
open import type-former.equiv
open import type-former.hlevels
open import type-former.path
open import type-former.pi
open import type-former.sigma
open import universe.core
open import universe.fibrant
open import universe.glue

module universe.univalence where

UATFib : âˆ€ (@â™­ â„“) â†’ TFibStr {Î“ = ğŸ™ â–·á¶  ğ‘¼á¶  â„“} (Î£Ë£ (ğ‘¼Ë£ â„“) (EquivË£ (ElË£ ğ’’) (ElË£ (ğ’’ âˆ˜ ğ’‘))))
UATFib â„“ (tt , B) Ï† Part = filler
  where
  ExtendedTy : ğ‘¼ â„“
  ExtendedTy = Glueáµ Ï† B (fst âˆ˜ Part) (snd âˆ˜ Part)

  extendedEquiv : Equiv (El ExtendedTy) (El B)
  extendedEquiv = unglueáµEquiv

  partEquiv : [ Ï† ] â†’ Equiv (El ExtendedTy) (El B)
  partEquiv u =
    subst (Equiv â¦…â€“â¦† (El B) âˆ˜ El) (GlueáµMatch _ _ _ _ _) (Part u .snd)

  partFunâ‰¡extendedFun : âˆ€ u â†’ partEquiv u .fst â‰¡ extendedEquiv .fst
  partFunâ‰¡extendedFun u =
    substNaturality (Î» _ â†’ fst) (GlueáµMatch _ _ _ _ _) âˆ™
    unglueáµMatch u

  fixPath : (u : [ Ï† ]) â†’ partEquiv u ~ extendedEquiv
  fixPath u =
    equivPathá¶  (Elá¶  (cst ExtendedTy)) (Elá¶  (cst B)) _ _
      (cst $ eqToPath $ partFunâ‰¡extendedFun u)
      tt

  box : OpenBox ğ•š 1 (cst (Î£ A âˆˆ ğ‘¼ â„“ , Equiv (El A) (El B)))
  box .cof = Ï†
  box .tube i u .fst = ExtendedTy
  box .tube i u .snd = fixPath u .at i
  box .cap .out .fst = ExtendedTy
  box .cap .out .snd = extendedEquiv
  box .cap .outâ‰¡ u = Î£ext refl (fixPath u .at1)

  filler : (Î£ A âˆˆ ğ‘¼ â„“ , Equiv (El A) (El B)) [ Ï† â†¦ Part ]
  filler =
    subst
      (_ [ Ï† â†¦_])
      (funExt Î» u â†’ sym (Î£ext (GlueáµMatch _ _ _ _ _) (sym (fixPath u .at0))))
      (Î£á¶  (ğ‘¼á¶  â„“) (Equivá¶  (Elá¶  snd) (Elá¶  fst)) .snd .lift ğ•š 1 (Î» _ â†’ B) box .fill 0)

UA : âˆ€ (@â™­ â„“) â†’ ğŸ™ âŠ¢á¶  Î á¶  (ğ‘¼á¶  â„“) (IsContrá¶  (Î£á¶  (ğ‘¼á¶  â„“) (Equivá¶  (Elá¶  ğ’’) (Elá¶  (ğ’’ âˆ˜ ğ’‘)))))
UA â„“ = Î»Ë£ $ TFibToIsContr (_ , UATFib â„“)
