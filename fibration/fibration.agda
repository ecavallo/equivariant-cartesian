{-

Defines fibration structures and fibrations.

-}
module fibration.fibration where

open import basic
open import internal-extensional-type-theory
open import axiom.cofibration
open import axiom.funext
open import axiom.shape
open import cofibration

private variable
  â„“ â„“' : Level
  Î“ Î” : Type â„“

infix  1 _âŠ¢á¶ Type_ _âŠ¢á¶ _
infixl 3 _â–·á¶ _
infixl 5 _âˆ˜á¶ Ë¢_ _âˆ˜á¶ _ _$á¶ _

------------------------------------------------------------------------------------------
-- Open boxes
------------------------------------------------------------------------------------------

record OpenBox (S : Shape) (A : âŸ¨ S âŸ© â†’ Type â„“) (r : âŸ¨ S âŸ©) : Type â„“
  where
  constructor makeBox
  field
    cof : Cof
    tube : (i : âŸ¨ S âŸ©) â†’ [ cof ] â†’ A i
    cap : A r [ cof â†¦ tube r ]

open OpenBox public

reshapeBox : âˆ€ {S T} (Ïƒ : ShapeHom S T) {r} {A : âŸ¨ T âŸ© â†’ Type â„“}
  â†’ OpenBox T A (âŸª Ïƒ âŸ« r) â†’ OpenBox S (A âˆ˜ âŸª Ïƒ âŸ«) r
reshapeBox Ïƒ box .cof = box .cof
reshapeBox Ïƒ box .tube = box .tube âˆ˜ âŸª Ïƒ âŸ«
reshapeBox Ïƒ box .cap = box .cap

addToTube : âˆ€ {S} {A : âŸ¨ S âŸ© â†’ Type â„“} {r}
  (box : OpenBox S A r)
  (Ï† : Cof)
  (t : (i : âŸ¨ S âŸ©) â†’ [ Ï† ] â†’ A i [ box .cof â†¦ box .tube i ])
  (matchCap : (v : [ Ï† ]) â†’ t r v .out â‰¡ box .cap .out)
  â†’ OpenBox S A r
addToTube box Ï† t matchCap .cof = box .cof âˆ¨ Ï†
addToTube box Ï† t matchCap .tube i =
  âˆ¨-rec (box .tube i) (out âˆ˜ t i) (Î» u v â†’ t i v .outâ‰¡ u)
addToTube box Ï† t matchCap .cap .out = box .cap .out
addToTube box Ï† t matchCap .cap .outâ‰¡ =
  âˆ¨-elimEq (box .cap .outâ‰¡) matchCap

boxToPartial : âˆ€ {S} {A : âŸ¨ S âŸ© â†’ Type â„“} {r} (box : OpenBox S A r)
  (s : âŸ¨ S âŸ©) â†’ [ box .cof âˆ¨ S âˆ‹ r â‰ˆ s ] â†’ A s
boxToPartial box s =
  âˆ¨-rec
    (box .tube s)
    (Î» {refl â†’ box .cap .out})
    (Î» {u refl â†’ box .cap .outâ‰¡ u})

partialToBox : âˆ€ {S} {A : âŸ¨ S âŸ© â†’ Type â„“} {r} (Ï† : Cof)
  â†’ ((s : âŸ¨ S âŸ©) â†’ [ Ï† âˆ¨ S âˆ‹ r â‰ˆ s ] â†’ A s)
  â†’ OpenBox S A r
partialToBox Ï† part .cof = Ï†
partialToBox Ï† part .tube i = part i âˆ˜ âˆ¨l
partialToBox Ï† part .cap .out = part _ (âˆ¨r refl)
partialToBox {S = S} {r = r} Ï† part .cap .outâ‰¡ u =
  cong (part _) (cofIsStrictProp' (Ï† âˆ¨ S âˆ‹ r â‰ˆ r))

reshapePartial : âˆ€ {S T} (Ïƒ : ShapeHom S T) {r} {Ï† : Cof}
  {A : (j : âŸ¨ T âŸ©) â†’ [ Ï† âˆ¨ T âˆ‹ âŸª Ïƒ âŸ« r â‰ˆ j ] â†’ Type â„“}
  â†’ ((j : âŸ¨ T âŸ©) (v : [ Ï† âˆ¨ T âˆ‹ âŸª Ïƒ âŸ« r â‰ˆ j ]) â†’ A j v)
  â†’ ((i : âŸ¨ S âŸ©) (u : [ Ï† âˆ¨ S âˆ‹ r â‰ˆ i ]) â†’ A (âŸª Ïƒ âŸ« i) ((id âˆ¨` cong âŸª Ïƒ âŸ«) u))
reshapePartial Ïƒ part i = part (âŸª Ïƒ âŸ« i) âˆ˜ (id âˆ¨` cong âŸª Ïƒ âŸ«)

opaque
  varyBoxToPartial : âˆ€ {S T} (Ïƒ : ShapeHom S T) {A : âŸ¨ T âŸ© â†’ Type â„“} {r}
    (box : OpenBox T A (âŸª Ïƒ âŸ« r))
    (s : âŸ¨ S âŸ©)
    (v : [ box .cof âˆ¨ T âˆ‹ âŸª Ïƒ âŸ« r â‰ˆ âŸª Ïƒ âŸ« s ])
    (u : [ box .cof âˆ¨ S âˆ‹ r â‰ˆ s ])
    â†’ boxToPartial box (âŸª Ïƒ âŸ« s) v â‰¡ boxToPartial (reshapeBox Ïƒ box) s u
  varyBoxToPartial {S = S} {T} Ïƒ {r = r} box s =
    takeOutCof (box .cof) (T âˆ‹ âŸª Ïƒ âŸ« r â‰ˆ âŸª Ïƒ âŸ« s) (S âˆ‹ r â‰ˆ s)
      (Î» u â†’ refl)
      (Î» {refl refl â†’ refl})

opaque
  boxExt : {S : Shape} {A : âŸ¨ S âŸ© â†’ Type â„“} {r : âŸ¨ S âŸ©}
    {box box' : OpenBox S A r}
    â†’ box .cof â‰¡ box' .cof
    â†’ (âˆ€ i u v â†’ box .tube i u â‰¡ box' .tube i v)
    â†’ box .cap .out â‰¡ box' .cap .out
    â†’ box â‰¡ box'
  boxExt {box = box} refl q refl =
    congÎ£ (Î» t c â†’ makeBox (box .cof) t (makeRestrict (box .cap .out) c))
      (funExt' $ funExt' $ q _ _ _)
      (funExt' uip')

opaque
  boxExtDep : {S : Shape} {B : Type â„“} {A : B â†’ âŸ¨ S âŸ© â†’ Type â„“'}
    {bâ‚€ bâ‚ : B} (b : bâ‚€ â‰¡ bâ‚)
    {r : âŸ¨ S âŸ©}
    {boxâ‚€ : OpenBox S (A bâ‚€) r} {boxâ‚ : OpenBox S (A bâ‚) r}
    â†’ boxâ‚€ .cof â‰¡ boxâ‚ .cof
    â†’ (âˆ€ i u v â†’ subst (Î» b' â†’ A b' i) b (boxâ‚€ .tube i u) â‰¡ boxâ‚ .tube i v)
    â†’ subst (A â¦…â€“â¦† r) b (boxâ‚€ .cap .out) â‰¡ boxâ‚ .cap .out
    â†’ subst (OpenBox S â¦…â€“â¦† r âˆ˜ A) b boxâ‚€ â‰¡ boxâ‚
  boxExtDep refl = boxExt

------------------------------------------------------------------------------------------
-- Solutions to individual lifting problems
------------------------------------------------------------------------------------------

record Filler {S : Shape} {A : âŸ¨ S âŸ© â†’ Type â„“} {r : âŸ¨ S âŸ©} (box : OpenBox S A r) : Type â„“
  where
  constructor makeFiller
  field
    fill : (s : âŸ¨ S âŸ©) â†’ A s [ box .cof â†¦ box .tube s ]
    capâ‰¡ : fill r .out â‰¡ box .cap .out

open Filler public

reshapeFiller : {S T : Shape} (Ïƒ : ShapeHom S T)
  {A : âŸ¨ T âŸ© â†’ Type â„“} {r : âŸ¨ S âŸ©}
  {box : OpenBox T A (âŸª Ïƒ âŸ« r)}
  â†’ Filler box
  â†’ Filler (reshapeBox Ïƒ box)
reshapeFiller Ïƒ w .fill = w .fill âˆ˜ âŸª Ïƒ âŸ«
reshapeFiller Ïƒ w .capâ‰¡ = w .capâ‰¡

opaque
  fillerExt : {S : Shape} {A : âŸ¨ S âŸ© â†’ Type â„“}  {r : âŸ¨ S âŸ©}
    {box : OpenBox S A r}
    {co co' : Filler box}
    â†’ (âˆ€ s â†’ co .fill s .out â‰¡ co' .fill s .out)
    â†’ co â‰¡ co'
  fillerExt p =
    congÎ£ makeFiller (funExt $ restrictExt âˆ˜ p) uip'

opaque
  fillerCong : {S : Shape} {A : âŸ¨ S âŸ© â†’ Type â„“} {r : âŸ¨ S âŸ©}
    {box : OpenBox S A r}
    {co co' : Filler box}
    â†’ co â‰¡ co'
    â†’ (âˆ€ s â†’ co .fill s .out â‰¡ co' .fill s .out)
  fillerCong p s = cong out (cong$ (cong fill p))

fitsPartialToFiller : âˆ€ {S} {A : âŸ¨ S âŸ© â†’ Type â„“} {r} {box : OpenBox S A r}
  â†’ ((s : âŸ¨ S âŸ©) â†’ A s [ box .cof âˆ¨ S âˆ‹ r â‰ˆ s â†¦ boxToPartial box s ])
  â†’ Filler box
fitsPartialToFiller total .fill s = narrow (total s) âˆ¨l
fitsPartialToFiller total .capâ‰¡ = sym (total _ .outâ‰¡ (âˆ¨r refl))

fillerToFitsPartial : âˆ€ {S} {A : âŸ¨ S âŸ© â†’ Type â„“} {r} {box : OpenBox S A r}
  â†’ Filler box
  â†’ ((s : âŸ¨ S âŸ©) â†’ A s [ box .cof âˆ¨ S âˆ‹ r â‰ˆ s â†¦ boxToPartial box s ])
fillerToFitsPartial filler s .out = filler .fill s .out
fillerToFitsPartial filler s .outâ‰¡ =
  âˆ¨-elimEq (filler .fill s .outâ‰¡) (Î» {refl â†’ sym (filler .capâ‰¡)})

------------------------------------------------------------------------------------------
-- Equivariant fibrations
------------------------------------------------------------------------------------------

--â†“ Type of operations filling open boxes over a given shape-indexed family.

CellFillStr : (S : Shape) (A : âŸ¨ S âŸ© â†’ Type â„“) â†’ Type â„“
CellFillStr S A = âˆ€ r (box : OpenBox S A r) â†’ Filler box

--â†“ Type of

FillStr : (S : Shape) {Î“ : Type â„“} (A : Î“ â†’ Type â„“') â†’ Type (â„“ âŠ” â„“')
FillStr S {Î“} A = (Î³ : Î“ ^ S) â†’ CellFillStr S (A âˆ˜ Î³)

--â†“ The equivariance condition on filling structures: for every shape homomorphism
--â†“ Ïƒ : S â†’ T, filling an open box over T and then composing with Ïƒ should be the
--â†“ same as composing the box with Ïƒ and then filling over S.

CellEquivariance : {S T : Shape} (Ïƒ : ShapeHom S T) {A : âŸ¨ T âŸ© â†’ Type â„“}
  â†’ CellFillStr T A â†’ CellFillStr S (A âˆ˜ âŸª Ïƒ âŸ«) â†’ Type â„“
CellEquivariance Ïƒ liftT liftS =
  âˆ€ r box s â†’
  reshapeFiller Ïƒ (liftT (âŸª Ïƒ âŸ« r) box) .fill s .out
  â‰¡ liftS r (reshapeBox Ïƒ box) .fill s .out

--â†“ Definition of an equivariant fibration structure.

record FibStr {Î“ : Type â„“} (A : Î“ â†’ Type â„“') : Type (â„“ âŠ” â„“') where
  constructor makeFib
  field
    --â†“ We have a filling structure for every shape.

    lift : (S : Shape) â†’ FillStr S A

    --â†“ The filling structures satisfy the equivariance condition.

    vary : âˆ€ S T (Ïƒ : ShapeHom S T) (Î³ : Î“ ^ T)
      â†’ CellEquivariance Ïƒ (lift T Î³) (lift S (Î³ âˆ˜ âŸª Ïƒ âŸ«))

open FibStr public

--â†“ Fibrant type judgment.

_âŠ¢á¶ Type_ : (Î“ : Type â„“) (â„“' : Level) â†’ Type (â„“ âŠ” lsuc â„“')
Î“ âŠ¢á¶ Type â„“' = Î£ (Î“ â†’ Type â„“') FibStr

--â†“ Convenient and/or suggestive notation for accessing the underlying family of a fibrant
--â†“ type and evaluating the family at some instantiation of the context (i.e., taking some
--â†“ fiber).

âˆ£_âˆ£ : (Î“ âŠ¢á¶ Type â„“) â†’ (Î“ â†’ Type â„“)
âˆ£ A âˆ£ = A .fst

_$á¶ _ : (Î“ âŠ¢á¶ Type â„“) â†’ Î“ â†’ Type â„“
A $á¶  Î³ = âˆ£ A âˆ£ Î³

--â†“ Term of a fibrant type.
--â†“ This is just a term of the underlying extensional type.

_âŠ¢á¶ _ : (Î“ : Type â„“) (A : Î“ âŠ¢á¶ Type â„“') â†’ Type (â„“ âŠ” â„“')
Î“ âŠ¢á¶  A = Î“ âŠ¢Ë£ âˆ£ A âˆ£

--â†“ Context extension by a fibrant type.
--â†“ This is just extension by the underlying extensional type.

_â–·á¶ _ : (Î“ : Type â„“) (A : Î“ âŠ¢á¶ Type â„“') â†’ Type (â„“ âŠ” â„“')
Î“ â–·á¶  A = Î“ â–·Ë£ âˆ£ A âˆ£

------------------------------------------------------------------------------------------
-- Reindexing fibration structures and fibrations
------------------------------------------------------------------------------------------

_âˆ˜á¶ Ë¢_ : {A : Î“ â†’ Type â„“} (Î± : FibStr A) (Ï : Î” â†’ Î“) â†’ FibStr (A âˆ˜ Ï)
(Î± âˆ˜á¶ Ë¢ Ï) .lift S Î³ r = Î± .lift S (Ï âˆ˜ Î³) r
(Î± âˆ˜á¶ Ë¢ Ï) .vary S T Ïƒ Î³ r = Î± .vary S T Ïƒ (Ï âˆ˜ Î³) r

_âˆ˜á¶ _ : (Î“ âŠ¢á¶ Type â„“) â†’ (Î” â†’ Î“) â†’ Î” âŠ¢á¶ Type â„“
(A âˆ˜á¶  Ï) .fst = A .fst âˆ˜ Ï
(A âˆ˜á¶  Ï) .snd = (A .snd) âˆ˜á¶ Ë¢ Ï

--â†“ Restriction of a fibration structure or fibration along a cofibration.

_â†¾á¶ Ë¢_ : {A : Î“ â†’ Type â„“} (Î± : FibStr A) (Ï† : Î“ â†’ Cof) â†’ FibStr (A â†¾ Ï†)
(Î± â†¾á¶ Ë¢ Ï†) = Î± âˆ˜á¶ Ë¢ ğ’‘

_â†¾á¶ _ : (A : Î“ âŠ¢á¶ Type â„“) (Ï† : Î“ â†’ Cof) â†’ Î“ â–·[ Ï† ] âŠ¢á¶ Type â„“
(A â†¾á¶  Ï†) = A âˆ˜á¶  ğ’‘

opaque
  reindexSubst : {A A' : Î“ â†’ Type â„“}
    (Î± : FibStr A) (P : A â‰¡ A') (Ï : Î” â†’ Î“) (Q : A âˆ˜ Ï â‰¡ A' âˆ˜ Ï)
    â†’ subst FibStr P Î± âˆ˜á¶ Ë¢ Ï â‰¡ subst FibStr Q (Î± âˆ˜á¶ Ë¢ Ï)
  reindexSubst Î± refl Ï refl = refl

------------------------------------------------------------------------------------------
-- Mapping boxes and fillers
------------------------------------------------------------------------------------------

mapBox : {S : Shape} {r : âŸ¨ S âŸ©}
  {A : âŸ¨ S âŸ© â†’ Type â„“} {B : âŸ¨ S âŸ© â†’ Type â„“'}
  â†’ (âˆ€ s â†’ A s â†’ B s)
  â†’ OpenBox S A r â†’ OpenBox S B r
mapBox f box .cof = box .cof
mapBox f box .tube i u = f i (box .tube i u)
mapBox f box .cap .out = f _ (box .cap .out)
mapBox f box .cap .outâ‰¡ u = cong (f _) (box .cap .outâ‰¡ u)

mapFiller : {S : Shape} {r : âŸ¨ S âŸ©}
  {A : âŸ¨ S âŸ© â†’ Type â„“} {B : âŸ¨ S âŸ© â†’ Type â„“'}
  (f : âˆ€ s â†’ A s â†’ B s)
  {box : OpenBox S A r}
  â†’ Filler box â†’ Filler (mapBox f box)
mapFiller f filler .fill s = mapRestrict (f s) (filler .fill s)
mapFiller f filler .capâ‰¡ = cong (f _) (filler .capâ‰¡)

------------------------------------------------------------------------------------------
-- Extensionality principle for fibrations
------------------------------------------------------------------------------------------

opaque
  FibStrExt : {A : Î“ â†’ Type â„“} {Î±â‚€ Î±â‚ : FibStr A}
    â†’ (âˆ€ S Î³ r box s â†’ Î±â‚€ .lift S Î³ r box .fill s .out â‰¡ Î±â‚ .lift S Î³ r box .fill s .out)
    â†’ Î±â‚€ â‰¡ Î±â‚
  FibStrExt q =
    congÎ£ makeFib
      (funExt' $ funExt' $ funExt' $ funExt' $ fillerExt $ q _ _ _ _)
      (funExt' $ funExt' $ funExt' $ funExt' $ funExt' $ funExt' $ funExt' uip')

------------------------------------------------------------------------------------------
-- A strict retract of a fibration is a fibration
------------------------------------------------------------------------------------------

RetractË£ : (A : Î“ â†’ Type â„“) (B : Î“ â†’ Type â„“') â†’ (Î“ â†’ Type (â„“ âŠ” â„“'))
RetractË£ A B Î³ = Retract (A Î³) (B Î³)

opaque
  retractFibStr : {A : Î“ â†’ Type â„“} {B : Î“ â†’ Type â„“'}
    â†’ Î“ âŠ¢Ë£ RetractË£ A B â†’ FibStr B â†’ FibStr A
  retractFibStr retract Î² .lift S Î³ r box = filler
    where
    fillerB : Filler (mapBox (sec âˆ˜ retract âˆ˜ Î³) box)
    fillerB = Î² .lift S Î³ r (mapBox (sec âˆ˜ retract âˆ˜ Î³) box)

    filler : Filler box
    filler .fill s .out = retract (Î³ s) .ret (fillerB .fill s .out)
    filler .fill s .outâ‰¡ u =
      sym (retract (Î³ s) .inv _)
      âˆ™ cong (retract (Î³ s) .ret) (fillerB .fill s .outâ‰¡ u)
    filler .capâ‰¡ =
      cong (retract (Î³ r) .ret) (fillerB .capâ‰¡)
      âˆ™ retract (Î³ r) .inv _

  retractFibStr retract Î² .vary S T Ïƒ Î³ r box s =
    cong (retract _ .ret) (Î² .vary S T Ïƒ Î³ r (mapBox (sec âˆ˜ retract âˆ˜ Î³) box) s)

opaque
  unfolding retractFibStr
  reindexRetractFibStr : {A : Î“ â†’ Type â„“} {B : Î“ â†’ Type â„“'}
    (retract : Î“ âŠ¢Ë£ RetractË£ A B) {Î² : FibStr B} (Ï : Î” â†’ Î“)
    â†’ retractFibStr retract Î² âˆ˜á¶ Ë¢ Ï  â‰¡ retractFibStr (retract âˆ˜ Ï) (Î² âˆ˜á¶ Ë¢ Ï)
  reindexRetractFibStr retract Ï = FibStrExt Î» _ _ _ _ _ â†’ refl

------------------------------------------------------------------------------------------
-- Corollary: fibration structures can be transferred across isomorphisms
------------------------------------------------------------------------------------------

_â‰…Ë£_ : (A : Î“ â†’ Type â„“) (B : Î“ â†’ Type â„“') â†’ (Î“ â†’ Type (â„“ âŠ” â„“'))
_â‰…Ë£_ A B Î³ = A Î³ â‰… B Î³

opaque
  isomorphFibStr : {A : Î“ â†’ Type â„“} {B : Î“ â†’ Type â„“'}
    â†’ Î“ âŠ¢Ë£ A â‰…Ë£ B â†’ FibStr B â†’ FibStr A
  isomorphFibStr iso Î² = retractFibStr (isoToRetract âˆ˜ iso) Î²

opaque
  unfolding isomorphFibStr
  reindexIsomorphFibStr : {A : Î“ â†’ Type â„“} {B : Î“ â†’ Type â„“'}
    (iso : Î“ âŠ¢Ë£ A â‰…Ë£ B) {Î² : FibStr B} (Ï : Î” â†’ Î“)
    â†’ isomorphFibStr iso Î² âˆ˜á¶ Ë¢ Ï â‰¡ isomorphFibStr (iso âˆ˜ Ï) (Î² âˆ˜á¶ Ë¢ Ï)
  reindexIsomorphFibStr _ = reindexRetractFibStr _
