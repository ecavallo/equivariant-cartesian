{-

Defines fibration structures and fibrations.

-}
module fibration.fibration where

open import basic
open import internal-extensional-type-theory
open import axiom
open import cofibration

private variable
  â„“ â„“' : Level
  Î“ Î” : Type â„“

infix  1 _âŠ¢á¶ Type_ _âŠ¢á¶ _
infixl 3 _â–·á¶ _
infixl 5 _âˆ˜á¶ Ë¢_ _âˆ˜á¶ _ _$á¶ _

------------------------------------------------------------------------------------------
-- Open boxes
------------------------------------------------------------------------------------------

record OpenBox (S : Shape) (A : âŸ¨ S âŸ© â†’ Type â„“) (r : âŸ¨ S âŸ©) : Type â„“
  where
  constructor makeBox
  field
    cof : Cof
    tube : (i : âŸ¨ S âŸ©) â†’ [ cof ] â†’ A i
    cap : A r [ cof â†¦ tube r ]

open OpenBox public

reshapeBox : âˆ€ {S T} (Ïƒ : ShapeHom S T) {r} {A : âŸ¨ T âŸ© â†’ Type â„“}
  â†’ OpenBox T A (âŸª Ïƒ âŸ« r) â†’ OpenBox S (A âˆ˜ âŸª Ïƒ âŸ«) r
reshapeBox Ïƒ box .cof = box .cof
reshapeBox Ïƒ box .tube = box .tube âˆ˜ âŸª Ïƒ âŸ«
reshapeBox Ïƒ box .cap = box .cap

mapBox : {S : Shape} {r : âŸ¨ S âŸ©}
  {A : âŸ¨ S âŸ© â†’ Type â„“} {B : âŸ¨ S âŸ© â†’ Type â„“'}
  â†’ (âˆ€ s â†’ A s â†’ B s)
  â†’ OpenBox S A r â†’ OpenBox S B r
mapBox f box .cof = box .cof
mapBox f box .tube i u = f i (box .tube i u)
mapBox f box .cap .out = f _ (box .cap .out)
mapBox f box .cap .outâ‰¡ u = cong (f _) (box .cap .outâ‰¡ u)

addToTube : âˆ€ {S} {A : âŸ¨ S âŸ© â†’ Type â„“} {r}
  (box : OpenBox S A r)
  (Ï† : Cof)
  (t : (i : âŸ¨ S âŸ©) â†’ [ Ï† ] â†’ A i [ box .cof â†¦ box .tube i ])
  (matchCap : (v : [ Ï† ]) â†’ t r v .out â‰¡ box .cap .out)
  â†’ OpenBox S A r
addToTube box Ï† t matchCap .cof = box .cof âˆ¨ Ï†
addToTube box Ï† t matchCap .tube i =
  âˆ¨-rec (box .tube i) (out âˆ˜ t i) (Î» u v â†’ t i v .outâ‰¡ u)
addToTube box Ï† t matchCap .cap .out = box .cap .out
addToTube box Ï† t matchCap .cap .outâ‰¡ =
  âˆ¨-elimEq (box .cap .outâ‰¡) matchCap

boxToPartial : âˆ€ {S} {A : âŸ¨ S âŸ© â†’ Type â„“} {r} (box : OpenBox S A r)
  (s : âŸ¨ S âŸ©) â†’ [ box .cof âˆ¨ S âˆ‹ r â‰ˆ s ] â†’ A s
boxToPartial box s =
  âˆ¨-rec
    (box .tube s)
    (Î» {refl â†’ box .cap .out})
    (Î» {u refl â†’ box .cap .outâ‰¡ u})

opaque
  varyBoxToPartial : âˆ€ {S T} (Ïƒ : ShapeHom S T) {A : âŸ¨ T âŸ© â†’ Type â„“} {r}
    (box : OpenBox T A (âŸª Ïƒ âŸ« r))
    (s : âŸ¨ S âŸ©)
    (v : [ box .cof âˆ¨ T âˆ‹ âŸª Ïƒ âŸ« r â‰ˆ âŸª Ïƒ âŸ« s ])
    (u : [ box .cof âˆ¨ S âˆ‹ r â‰ˆ s ])
    â†’ boxToPartial box (âŸª Ïƒ âŸ« s) v â‰¡ boxToPartial (reshapeBox Ïƒ box) s u
  varyBoxToPartial {S = S} {T} Ïƒ {r = r} box s =
    takeOutCof (box .cof) (T âˆ‹ âŸª Ïƒ âŸ« r â‰ˆ âŸª Ïƒ âŸ« s)(S âˆ‹ r â‰ˆ s)
      (Î» u â†’ refl)
      (Î» {refl refl â†’ refl})

opaque
  boxExt : {S : Shape} {A : âŸ¨ S âŸ© â†’ Type â„“} {r : âŸ¨ S âŸ©}
    {box box' : OpenBox S A r}
    â†’ box .cof â‰¡ box' .cof
    â†’ (âˆ€ i u v â†’ box .tube i u â‰¡ box' .tube i v)
    â†’ box .cap .out â‰¡ box' .cap .out
    â†’ box â‰¡ box'
  boxExt {box = box} refl q refl =
    congÎ£ (Î» t c â†’ makeBox (box .cof) t (makeRestrict (box .cap .out) c))
      (funExt' $ funExt' $ q _ _ _)
      (funExt' uip')

opaque
  boxExtDep : {S : Shape} {B : Type â„“} {A : B â†’ âŸ¨ S âŸ© â†’ Type â„“'}
    {bâ‚€ bâ‚ : B} (b : bâ‚€ â‰¡ bâ‚)
    {r : âŸ¨ S âŸ©}
    {boxâ‚€ : OpenBox S (A bâ‚€) r} {boxâ‚ : OpenBox S (A bâ‚) r}
    â†’ boxâ‚€ .cof â‰¡ boxâ‚ .cof
    â†’ (âˆ€ i u v â†’ subst (Î» b' â†’ A b' i) b (boxâ‚€ .tube i u) â‰¡ boxâ‚ .tube i v)
    â†’ subst (A â¦…â€“â¦† r) b (boxâ‚€ .cap .out) â‰¡ boxâ‚ .cap .out
    â†’ subst (OpenBox S â¦…â€“â¦† r âˆ˜ A) b boxâ‚€ â‰¡ boxâ‚
  boxExtDep refl = boxExt

------------------------------------------------------------------------------------------
-- Solutions to individual lifting problems
------------------------------------------------------------------------------------------

record Filler {S : Shape} {A : âŸ¨ S âŸ© â†’ Type â„“} {r : âŸ¨ S âŸ©} (box : OpenBox S A r) : Type â„“
  where
  constructor makeFiller
  field
    fill : (s : âŸ¨ S âŸ©) â†’ A s [ box .cof â†¦ box .tube s ]
    capâ‰¡ : fill r .out â‰¡ box .cap .out

open Filler public

reshapeFiller : {S T : Shape} (Ïƒ : ShapeHom S T)
  {A : âŸ¨ T âŸ© â†’ Type â„“} {r : âŸ¨ S âŸ©}
  {box : OpenBox T A (âŸª Ïƒ âŸ« r)}
  â†’ Filler box
  â†’ Filler (reshapeBox Ïƒ box)
reshapeFiller Ïƒ w .fill = w .fill âˆ˜ âŸª Ïƒ âŸ«
reshapeFiller Ïƒ w .capâ‰¡ = w .capâ‰¡

opaque
  fillerExt : {S : Shape} {A : âŸ¨ S âŸ© â†’ Type â„“}  {r : âŸ¨ S âŸ©}
    {box : OpenBox S A r}
    {co co' : Filler box}
    â†’ (âˆ€ s â†’ co .fill s .out â‰¡ co' .fill s .out)
    â†’ co â‰¡ co'
  fillerExt p =
    congÎ£ makeFiller (funExt $ restrictExt âˆ˜ p) uip'

opaque
  fillerCong : {S : Shape} {A : âŸ¨ S âŸ© â†’ Type â„“} {r : âŸ¨ S âŸ©}
    {box : OpenBox S A r}
    {co co' : Filler box}
    â†’ co â‰¡ co'
    â†’ (âˆ€ s â†’ co .fill s .out â‰¡ co' .fill s .out)
  fillerCong p s = cong out (cong$ (cong fill p))

fitsPartialToFiller : âˆ€ {S} {A : âŸ¨ S âŸ© â†’ Type â„“} {r} {box : OpenBox S A r}
  â†’ ((s : âŸ¨ S âŸ©) â†’ A s [ box .cof âˆ¨ S âˆ‹ r â‰ˆ s â†¦ boxToPartial box s ])
  â†’ Filler box
fitsPartialToFiller filler .fill s = narrow (filler s) âˆ¨l
fitsPartialToFiller filler .capâ‰¡ = sym (filler _ .outâ‰¡ (âˆ¨r refl))

------------------------------------------------------------------------------------------
-- Equivariant fibrations
------------------------------------------------------------------------------------------

--â†“ Definition of an equivariant fibration structure.

record FibStr {Î“ : Type â„“} (A : Î“ â†’ Type â„“') : Type (â„“ âŠ” â„“') where
  constructor makeFib
  field
    --â†“ For every shape S, map p : âŸ¨ S âŸ© â†’ Î“, and open box over p, we have a chosen lift.

    lift : (S : Shape) (p : âŸ¨ S âŸ© â†’ Î“) (r : âŸ¨ S âŸ©)
      (box : OpenBox S (A âˆ˜ p) r) â†’ Filler box

    --â†“ The equivariance condition on lifts: for every shape homomorphism and open box,
    --â†“ reshaping the open box and then lifting has the same effect as lifting and then
    --â†“ reshaping the filler.

    vary : âˆ€ S T (Ïƒ : ShapeHom S T) (p : âŸ¨ T âŸ© â†’ Î“) (r : âŸ¨ S âŸ©)
      (box : OpenBox T (A âˆ˜ p) (âŸª Ïƒ âŸ« r)) (s : âŸ¨ S âŸ©)
      â†’ reshapeFiller Ïƒ (lift T p (âŸª Ïƒ âŸ« r) box) .fill s .out
        â‰¡ lift S (p âˆ˜ âŸª Ïƒ âŸ«) r (reshapeBox Ïƒ box) .fill s .out

open FibStr public

--â†“ Fibrant type judgment.

_âŠ¢á¶ Type_ : (Î“ : Type â„“) (â„“' : Level) â†’ Type (â„“ âŠ” lsuc â„“')
Î“ âŠ¢á¶ Type â„“' = Î£ (Î“ â†’ Type â„“') FibStr

--â†“ Convenient and/or suggestive notation for accessing the underlying family of a fibrant
--â†“ type and evaluating the family at some instantiation of the context (i.e., taking some
--â†“ fiber).

âˆ£_âˆ£ : (Î“ âŠ¢á¶ Type â„“) â†’ (Î“ â†’ Type â„“)
âˆ£ A âˆ£ = A .fst

_$á¶ _ : (Î“ âŠ¢á¶ Type â„“) â†’ Î“ â†’ Type â„“
A $á¶  Î³ = âˆ£ A âˆ£ Î³

--â†“ Term of a fibrant type.
--â†“ This is just a term of the underlying extensional type.

_âŠ¢á¶ _ : (Î“ : Type â„“) (A : Î“ âŠ¢á¶ Type â„“') â†’ Type (â„“ âŠ” â„“')
Î“ âŠ¢á¶  A = Î“ âŠ¢Ë£ âˆ£ A âˆ£

--â†“ Context extension by a fibrant type.
--â†“ This is just extension by the underlying extensional type.

_â–·á¶ _ : (Î“ : Type â„“) (A : Î“ âŠ¢á¶ Type â„“') â†’ Type (â„“ âŠ” â„“')
Î“ â–·á¶  A = Î“ â–·Ë£ âˆ£ A âˆ£

------------------------------------------------------------------------------------------
-- Reindexing fibration structures and fibrations
------------------------------------------------------------------------------------------

_âˆ˜á¶ Ë¢_ : {A : Î“ â†’ Type â„“} (Î± : FibStr A) (Ï : Î” â†’ Î“) â†’ FibStr (A âˆ˜ Ï)
(Î± âˆ˜á¶ Ë¢ Ï) .lift S p r = Î± .lift S (Ï âˆ˜ p) r
(Î± âˆ˜á¶ Ë¢ Ï) .vary S T Ïƒ p r = Î± .vary S T Ïƒ (Ï âˆ˜ p) r

_âˆ˜á¶ _ : (Î“ âŠ¢á¶ Type â„“) â†’ (Î” â†’ Î“) â†’ Î” âŠ¢á¶ Type â„“
(A âˆ˜á¶  Ï) .fst = A .fst âˆ˜ Ï
(A âˆ˜á¶  Ï) .snd = (A .snd) âˆ˜á¶ Ë¢ Ï

--â†“ Restriction of a fibration structure or fibration along a cofibration.

_â†¾á¶ Ë¢_ : {A : Î“ â†’ Type â„“} (Î± : FibStr A) (Ï† : Î“ â†’ Cof) â†’ FibStr (A â†¾ Ï†)
(Î± â†¾á¶ Ë¢ Ï†) = Î± âˆ˜á¶ Ë¢ ğ’‘

_â†¾á¶ _ : (A : Î“ âŠ¢á¶ Type â„“) (Ï† : Î“ â†’ Cof) â†’ Î“ â–·[ Ï† ] âŠ¢á¶ Type â„“
(A â†¾á¶  Ï†) = A âˆ˜á¶  ğ’‘

opaque
  reindexSubst : {A A' : Î“ â†’ Type â„“}
    (Î± : FibStr A) (P : A â‰¡ A') (Ï : Î” â†’ Î“) (Q : A âˆ˜ Ï â‰¡ A' âˆ˜ Ï)
    â†’ subst FibStr P Î± âˆ˜á¶ Ë¢ Ï â‰¡ subst FibStr Q (Î± âˆ˜á¶ Ë¢ Ï)
  reindexSubst Î± refl Ï refl = refl

------------------------------------------------------------------------------------------
-- Extensionality principles for fibrations
------------------------------------------------------------------------------------------

FibStrEq : {Î“ : Type â„“} {A : Î“ â†’ Type â„“'} (Î±â‚€ Î±â‚ : FibStr A) â†’ Type (â„“ âŠ” â„“')
FibStrEq {Î“ = Î“} {A = A} Î±â‚€ Î±â‚ =
  ((S : Shape) (p : âŸ¨ S âŸ© â†’ Î“) (r : âŸ¨ S âŸ©)
  (box : OpenBox S (A âˆ˜ p) r)
  (s : âŸ¨ S âŸ©) â†’ Î±â‚€ .lift S p r box .fill s .out â‰¡ Î±â‚ .lift S p r box .fill s .out)

opaque
  FibStrExt : {A : Î“ â†’ Type â„“} {Î± Î±' : FibStr A} â†’ FibStrEq Î± Î±' â†’ Î± â‰¡ Î±'
  FibStrExt q =
    congÎ£ makeFib
      (funExt' $ funExt' $ funExt' $ funExt' $ fillerExt $ q _ _ _ _)
      (funExt' $ funExt' $ funExt' $ funExt' $ funExt' $ funExt' $ funExt' uip')

------------------------------------------------------------------------------------------
-- A retract of a fibration is a fibration
------------------------------------------------------------------------------------------

RetractË£ : (A : Î“ â†’ Type â„“) (B : Î“ â†’ Type â„“') â†’ (Î“ â†’ Type (â„“ âŠ” â„“'))
RetractË£ A B Î³ = Retract (A Î³) (B Î³)

opaque
  retractFibStr : {A : Î“ â†’ Type â„“} {B : Î“ â†’ Type â„“'}
    â†’ Î“ âŠ¢Ë£ RetractË£ A B â†’ FibStr B â†’ FibStr A
  retractFibStr retract Î² .lift S p r box = filler
    where
    fillerB : Filler (mapBox (sec âˆ˜ retract âˆ˜ p) box)
    fillerB = Î² .lift S p r (mapBox (sec âˆ˜ retract âˆ˜ p) box)

    filler : Filler box
    filler .fill s .out = retract (p s) .ret (fillerB .fill s .out)
    filler .fill s .outâ‰¡ u =
      sym (retract (p s) .inv _)
      âˆ™ cong (retract (p s) .ret) (fillerB .fill s .outâ‰¡ u)
    filler .capâ‰¡ =
      cong (retract (p r) .ret) (fillerB .capâ‰¡)
      âˆ™ retract (p r) .inv _

  retractFibStr retract Î² .vary S T Ïƒ p r box s =
    cong (retract _ .ret) (Î² .vary S T Ïƒ p r (mapBox (sec âˆ˜ retract âˆ˜ p) box) s)

opaque
  unfolding retractFibStr
  reindexRetractFibStr : {A : Î“ â†’ Type â„“} {B : Î“ â†’ Type â„“'}
    (retract : Î“ âŠ¢Ë£ RetractË£ A B) {Î² : FibStr B} (Ï : Î” â†’ Î“)
    â†’ retractFibStr retract Î² âˆ˜á¶ Ë¢ Ï  â‰¡ retractFibStr (retract âˆ˜ Ï) (Î² âˆ˜á¶ Ë¢ Ï)
  reindexRetractFibStr retract Ï = FibStrExt Î» _ _ _ _ _ â†’ refl

------------------------------------------------------------------------------------------
-- Corollary: fibration structures can be transferred across isomorphisms
------------------------------------------------------------------------------------------

_â‰…Ë£_ : (A : Î“ â†’ Type â„“) (B : Î“ â†’ Type â„“') â†’ (Î“ â†’ Type (â„“ âŠ” â„“'))
_â‰…Ë£_ A B Î³ = A Î³ â‰… B Î³

opaque
  isomorphFibStr : {A : Î“ â†’ Type â„“} {B : Î“ â†’ Type â„“'}
    â†’ Î“ âŠ¢Ë£ A â‰…Ë£ B â†’ FibStr B â†’ FibStr A
  isomorphFibStr iso Î² = retractFibStr (isoToRetract âˆ˜ iso) Î²

opaque
  unfolding isomorphFibStr
  reindexIsomorphFibStr : {A : Î“ â†’ Type â„“} {B : Î“ â†’ Type â„“'}
    (iso : Î“ âŠ¢Ë£ A â‰…Ë£ B) {Î² : FibStr B} (Ï : Î” â†’ Î“)
    â†’ isomorphFibStr iso Î² âˆ˜á¶ Ë¢ Ï â‰¡ isomorphFibStr (iso âˆ˜ Ï) (Î² âˆ˜á¶ Ë¢ Ï)
  reindexIsomorphFibStr _ = reindexRetractFibStr _
