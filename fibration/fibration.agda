{-

Defines fibration structures and fibrations.

-}
module fibration.fibration where

open import basic
open import internal-extensional-type-theory
open import axiom.cofibration
open import axiom.funext
open import axiom.shape
open import cofibration

private variable
  â„“ â„“' : Level
  Î“ Î” : Type â„“

infix  1 _âŠ¢á¶ Type_ _âŠ¢á¶ _
infixl 3 _â–·á¶ _
infixl 5 _âˆ˜á¶ Ë¢_ _âˆ˜á¶ _ _$á¶ _

------------------------------------------------------------------------------------------
-- Open boxes
------------------------------------------------------------------------------------------

--â†“ The type of open boxes for a given shape S with the cap at a point r of S. An open box
--â†“ consists of a cofibration, a partially defined element indexed by S (the "tube"), and
--â†“ a total element (the "cap") agreeing with the tube at r.

record OpenBox (S : Shape) (A : âŸ¨ S âŸ© â†’ Type â„“) (r : âŸ¨ S âŸ©) : Type â„“
  where
  constructor makeBox
  field
    cof : Cof
    tube : (i : âŸ¨ S âŸ©) â†’ [ cof ] â†’ A i
    cap : A r [ cof â†¦ tube r ]

open OpenBox public

--â†“ A shape homomorphism Ïƒ : S â†’ T induces a map from T-boxes to S-boxes by
--â†“ precomposition.

reshapeBox : âˆ€ {S T} (Ïƒ : Shape[ S , T ]) {r} {A : âŸ¨ T âŸ© â†’ Type â„“}
  â†’ OpenBox T A (âŸª Ïƒ âŸ« r) â†’ OpenBox S (A âˆ˜ âŸª Ïƒ âŸ«) r
reshapeBox Ïƒ box .cof = box .cof
reshapeBox Ïƒ box .tube = box .tube âˆ˜ âŸª Ïƒ âŸ«
reshapeBox Ïƒ box .cap = box .cap

--â†“ Convenience function extending the tube of a box with another partial element.

addToTube : âˆ€ {S} {A : âŸ¨ S âŸ© â†’ Type â„“} {r}
  (box : OpenBox S A r)
  (Ï† : Cof)
  (t : (i : âŸ¨ S âŸ©) â†’ [ Ï† ] â†’ A i [ box .cof â†¦ box .tube i ])
  (matchCap : (v : [ Ï† ]) â†’ t r v .out â‰¡ box .cap .out)
  â†’ OpenBox S A r
addToTube box Ï† t matchCap .cof = box .cof âˆ¨ Ï†
addToTube box Ï† t matchCap .tube i =
  âˆ¨-rec (box .tube i) (out âˆ˜ t i) (Î» u v â†’ t i v .outâ‰¡ u)
addToTube box Ï† t matchCap .cap .out = box .cap .out
addToTube box Ï† t matchCap .cap .outâ‰¡ =
  âˆ¨-elimEq (box .cap .outâ‰¡) matchCap

------------------------------------------------------------------------------------------
-- Open boxes as partial elements.
------------------------------------------------------------------------------------------

--â†“ An S-box with cofibration Ï† and cap at r can also be encoded as a family over
--â†“ over s : S defined on Ï† âˆ¨ r â‰ˆ s. This representation is sometimes more convenient.

boxToPartial : âˆ€ {S} {A : âŸ¨ S âŸ© â†’ Type â„“} {r} (box : OpenBox S A r)
  (s : âŸ¨ S âŸ©) â†’ [ box .cof âˆ¨ S âˆ‹ r â‰ˆ s ] â†’ A s
boxToPartial box s =
  âˆ¨-rec
    (box .tube s)
    (Î» {refl â†’ box .cap .out})
    (Î» {u refl â†’ box .cap .outâ‰¡ u})

partialToBox : âˆ€ {S} {A : âŸ¨ S âŸ© â†’ Type â„“} {r} (Ï† : Cof)
  â†’ ((s : âŸ¨ S âŸ©) â†’ [ Ï† âˆ¨ S âˆ‹ r â‰ˆ s ] â†’ A s)
  â†’ OpenBox S A r
partialToBox Ï† part .cof = Ï†
partialToBox Ï† part .tube i = part i âˆ˜ âˆ¨l
partialToBox Ï† part .cap .out = part _ (âˆ¨r refl)
partialToBox {S = S} {r = r} Ï† part .cap .outâ‰¡ u =
  cong (part _) (cofIsStrictProp' (Ï† âˆ¨ S âˆ‹ r â‰ˆ r))

--â†“ Action of shape homomorphisms on open boxes encoded as partial elements.

reshapePartial : âˆ€ {S T} (Ïƒ : Shape[ S , T ]) {r} {Ï† : Cof}
  {A : (j : âŸ¨ T âŸ©) â†’ [ Ï† âˆ¨ T âˆ‹ âŸª Ïƒ âŸ« r â‰ˆ j ] â†’ Type â„“}
  â†’ ((j : âŸ¨ T âŸ©) (v : [ Ï† âˆ¨ T âˆ‹ âŸª Ïƒ âŸ« r â‰ˆ j ]) â†’ A j v)
  â†’ ((i : âŸ¨ S âŸ©) (u : [ Ï† âˆ¨ S âˆ‹ r â‰ˆ i ]) â†’ A (âŸª Ïƒ âŸ« i) ((id âˆ¨` cong âŸª Ïƒ âŸ«) u))
reshapePartial Ïƒ part i = part (âŸª Ïƒ âŸ« i) âˆ˜ (id âˆ¨` cong âŸª Ïƒ âŸ«)

--â†“ Reshaping open boxes commutes with the conversion from the record to the
--â†“ partial-element representation.

opaque
  reshapeBoxToPartial : âˆ€ {S T} (Ïƒ : Shape[ S , T ]) {A : âŸ¨ T âŸ© â†’ Type â„“} {r}
    (box : OpenBox T A (âŸª Ïƒ âŸ« r))
    (s : âŸ¨ S âŸ©)
    (v : [ box .cof âˆ¨ T âˆ‹ âŸª Ïƒ âŸ« r â‰ˆ âŸª Ïƒ âŸ« s ])
    (u : [ box .cof âˆ¨ S âˆ‹ r â‰ˆ s ])
    â†’ boxToPartial box (âŸª Ïƒ âŸ« s) v â‰¡ boxToPartial (reshapeBox Ïƒ box) s u
  reshapeBoxToPartial {S = S} {T} Ïƒ {r = r} box s =
    takeOutCof (box .cof) (T âˆ‹ âŸª Ïƒ âŸ« r â‰ˆ âŸª Ïƒ âŸ« s) (S âˆ‹ r â‰ˆ s)
      (Î» u â†’ refl)
      (Î» {refl refl â†’ refl})

------------------------------------------------------------------------------------------
-- Solutions to individual lifting problems.
------------------------------------------------------------------------------------------

--â†“ A filler for an S-box over A is a section of A that restricts to its tube and cap
--â†“ where they are defined.

record Filler {S : Shape} {A : âŸ¨ S âŸ© â†’ Type â„“} {r : âŸ¨ S âŸ©} (box : OpenBox S A r) : Type â„“
  where
  constructor makeFiller
  field
    fill : (s : âŸ¨ S âŸ©) â†’ A s [ box .cof â†¦ box .tube s ]
    capâ‰¡ : fill r .out â‰¡ box .cap .out

open Filler public

--â†“ A shape homomorphism Ïƒ : S â†’ T induces a map from T-fillers to S-fillers by
--â†“ precomposition.

reshapeFiller : {S T : Shape} (Ïƒ : Shape[ S , T ])
  {A : âŸ¨ T âŸ© â†’ Type â„“} {r : âŸ¨ S âŸ©}
  {box : OpenBox T A (âŸª Ïƒ âŸ« r)}
  â†’ Filler box
  â†’ Filler (reshapeBox Ïƒ box)
reshapeFiller Ïƒ w .fill = w .fill âˆ˜ âŸª Ïƒ âŸ«
reshapeFiller Ïƒ w .capâ‰¡ = w .capâ‰¡

--â†“ A total section fitting an open box encoded as a partial section is the same
--â†“ a filler for the open box encoded as a record.

fitsPartialToFiller : âˆ€ {S} {A : âŸ¨ S âŸ© â†’ Type â„“} {r} {box : OpenBox S A r}
  â†’ ((s : âŸ¨ S âŸ©) â†’ A s [ box .cof âˆ¨ S âˆ‹ r â‰ˆ s â†¦ boxToPartial box s ])
  â†’ Filler box
fitsPartialToFiller total .fill s = narrow (total s) âˆ¨l
fitsPartialToFiller total .capâ‰¡ = sym (total _ .outâ‰¡ (âˆ¨r refl))

fillerToFitsPartial : âˆ€ {S} {A : âŸ¨ S âŸ© â†’ Type â„“} {r} {box : OpenBox S A r}
  â†’ Filler box
  â†’ ((s : âŸ¨ S âŸ©) â†’ A s [ box .cof âˆ¨ S âˆ‹ r â‰ˆ s â†¦ boxToPartial box s ])
fillerToFitsPartial filler s .out = filler .fill s .out
fillerToFitsPartial filler s .outâ‰¡ =
  âˆ¨-elimEq (filler .fill s .outâ‰¡) (Î» {refl â†’ sym (filler .capâ‰¡)})

------------------------------------------------------------------------------------------
-- Equivariant fibrations.
------------------------------------------------------------------------------------------

--â†“ Type of operations filling open boxes over a given shape-indexed family.

LocalFillStr : (S : Shape) (A : âŸ¨ S âŸ© â†’ Type â„“) â†’ Type â„“
LocalFillStr S A = âˆ€ r (box : OpenBox S A r) â†’ Filler box

--â†“ A filling structure on a family consists of a local filling structure for every
--â†“ reindexing of the family over a shape. This would be the definition of fibration in
--â†“ a "non-equivariant fibration" model.

FillStr : (S : Shape) {Î“ : Type â„“} (A : Î“ â†’ Type â„“') â†’ Type (â„“ âŠ” â„“')
FillStr S {Î“} A = (Î³ : Î“ ^ S) â†’ LocalFillStr S (A âˆ˜ Î³)

--â†“ The equivariance condition on local filling structures associated to a shape
--â†“ homomorphism Ïƒ : S â†’ T. Filling an open box over T and then composing with Ïƒ should be
--â†“ the same as composing the box with Ïƒ and then filling over S.

LocalEquivariance : {S T : Shape} (Ïƒ : Shape[ S , T ]) {A : âŸ¨ T âŸ© â†’ Type â„“}
  â†’ LocalFillStr T A â†’ LocalFillStr S (A âˆ˜ âŸª Ïƒ âŸ«) â†’ Type â„“
LocalEquivariance Ïƒ liftT liftS =
  âˆ€ r box s â†’
  reshapeFiller Ïƒ (liftT (âŸª Ïƒ âŸ« r) box) .fill s .out
  â‰¡ liftS r (reshapeBox Ïƒ box) .fill s .out

Equivariance : {S T : Shape} (Ïƒ : Shape[ S , T ]) {Î“ : Type â„“} (A : Î“ â†’ Type â„“')
  â†’ FillStr T A â†’ FillStr S A â†’ Type (â„“ âŠ” â„“')
Equivariance {T = T} Ïƒ {Î“} A fillT fillS =
  (Î³ : Î“ ^ T) â†’ LocalEquivariance Ïƒ (fillT Î³) (fillS (Î³ âˆ˜ âŸª Ïƒ âŸ«))

--â†“ Definition of an equivariant fibration structure.

record FibStr {Î“ : Type â„“} (A : Î“ â†’ Type â„“') : Type (â„“ âŠ” â„“') where
  constructor makeFib
  field
    --â†“ We have a filling structure for every shape.

    lift : (S : Shape) â†’ FillStr S A

    --â†“ The filling structures satisfy the equivariance condition.

    vary : âˆ€ S T (Ïƒ : Shape[ S , T ]) â†’ Equivariance Ïƒ A (lift T) (lift S)

open FibStr public

--â†“ Fibrant type judgment.

_âŠ¢á¶ Type_ : (Î“ : Type â„“) (â„“' : Level) â†’ Type (â„“ âŠ” lsuc â„“')
Î“ âŠ¢á¶ Type â„“' = Î£ (Î“ â†’ Type â„“') FibStr

--â†“ Convenient and/or suggestive notation for accessing the underlying family of a fibrant
--â†“ type and evaluating the family at some instantiation of the context (i.e., taking some
--â†“ fiber).

âˆ£_âˆ£ : (Î“ âŠ¢á¶ Type â„“) â†’ (Î“ â†’ Type â„“)
âˆ£ A âˆ£ = A .fst

_$á¶ _ : (Î“ âŠ¢á¶ Type â„“) â†’ Î“ â†’ Type â„“
A $á¶  Î³ = âˆ£ A âˆ£ Î³

--â†“ Term of a fibrant type.
--â†“ This is just a term of the underlying extensional type.

_âŠ¢á¶ _ : (Î“ : Type â„“) (A : Î“ âŠ¢á¶ Type â„“') â†’ Type (â„“ âŠ” â„“')
Î“ âŠ¢á¶  A = Î“ âŠ¢Ë£ âˆ£ A âˆ£

--â†“ Context extension by a fibrant type.
--â†“ This is just extension by the underlying extensional type.

_â–·á¶ _ : (Î“ : Type â„“) (A : Î“ âŠ¢á¶ Type â„“') â†’ Type (â„“ âŠ” â„“')
Î“ â–·á¶  A = Î“ â–·Ë£ âˆ£ A âˆ£

------------------------------------------------------------------------------------------
-- Reindexing fibration structures and fibrations
------------------------------------------------------------------------------------------

--â†“ Reindexing of fibration structures.

_âˆ˜á¶ Ë¢_ : {A : Î“ â†’ Type â„“} (Î± : FibStr A) (Ï : Î” â†’ Î“) â†’ FibStr (A âˆ˜ Ï)
(Î± âˆ˜á¶ Ë¢ Ï) .lift S Î³ r = Î± .lift S (Ï âˆ˜ Î³) r
(Î± âˆ˜á¶ Ë¢ Ï) .vary S T Ïƒ Î³ r = Î± .vary S T Ïƒ (Ï âˆ˜ Î³) r

--â†“ Reindexing of fibrations.

_âˆ˜á¶ _ : (Î“ âŠ¢á¶ Type â„“) â†’ (Î” â†’ Î“) â†’ Î” âŠ¢á¶ Type â„“
(A âˆ˜á¶  Ï) .fst = A .fst âˆ˜ Ï
(A âˆ˜á¶  Ï) .snd = (A .snd) âˆ˜á¶ Ë¢ Ï

--â†“ Restriction of a fibration structure or fibration along a cofibration.

_â†¾á¶ Ë¢_ : {A : Î“ â†’ Type â„“} (Î± : FibStr A) (Ï† : Î“ â†’ Cof) â†’ FibStr (A â†¾ Ï†)
(Î± â†¾á¶ Ë¢ Ï†) = Î± âˆ˜á¶ Ë¢ ğ’‘

_â†¾á¶ _ : (A : Î“ âŠ¢á¶ Type â„“) (Ï† : Î“ â†’ Cof) â†’ Î“ â–·[ Ï† ] âŠ¢á¶ Type â„“
(A â†¾á¶  Ï†) = A âˆ˜á¶  ğ’‘

--â†“ Reindexing fibration structures commutes with substitution.

opaque
  reindexSubst : {A A' : Î“ â†’ Type â„“}
    (Î± : FibStr A) (P : A â‰¡ A') (Ï : Î” â†’ Î“) (Q : A âˆ˜ Ï â‰¡ A' âˆ˜ Ï)
    â†’ subst FibStr P Î± âˆ˜á¶ Ë¢ Ï â‰¡ subst FibStr Q (Î± âˆ˜á¶ Ë¢ Ï)
  reindexSubst Î± refl Ï refl = refl

------------------------------------------------------------------------------------------
-- Mapping boxes and fillers.
------------------------------------------------------------------------------------------

mapBox : {S : Shape} {r : âŸ¨ S âŸ©}
  {A : âŸ¨ S âŸ© â†’ Type â„“} {B : âŸ¨ S âŸ© â†’ Type â„“'}
  â†’ (âˆ€ {s} â†’ A s â†’ B s)
  â†’ OpenBox S A r â†’ OpenBox S B r
mapBox f box .cof = box .cof
mapBox f box .tube i u = f (box .tube i u)
mapBox f box .cap .out = f (box .cap .out)
mapBox f box .cap .outâ‰¡ u = cong f (box .cap .outâ‰¡ u)

mapFiller : {S : Shape} {r : âŸ¨ S âŸ©}
  {A : âŸ¨ S âŸ© â†’ Type â„“} {B : âŸ¨ S âŸ© â†’ Type â„“'}
  (f : âˆ€ {s} â†’ A s â†’ B s)
  {box : OpenBox S A r}
  â†’ Filler box â†’ Filler (mapBox f box)
mapFiller f filler .fill s = mapRestrict f (filler .fill s)
mapFiller f filler .capâ‰¡ = cong f (filler .capâ‰¡)

------------------------------------------------------------------------------------------
-- Extensionality principles.
------------------------------------------------------------------------------------------

opaque
  boxExt : {S : Shape} {A : âŸ¨ S âŸ© â†’ Type â„“} {r : âŸ¨ S âŸ©}
    {boxâ‚€ boxâ‚ : OpenBox S A r}
    â†’ boxâ‚€ .cof â‰¡ boxâ‚ .cof
    â†’ (âˆ€ i u v â†’ boxâ‚€ .tube i u â‰¡ boxâ‚ .tube i v)
    â†’ boxâ‚€ .cap .out â‰¡ boxâ‚ .cap .out
    â†’ boxâ‚€ â‰¡ boxâ‚
  boxExt {boxâ‚€ = boxâ‚€} refl q refl =
    congÎ£ (Î» t c â†’ makeBox (boxâ‚€ .cof) t (makeRestrict (boxâ‚€ .cap .out) c))
      (funExt' $ funExt' $ q _ _ _)
      (funExt' uip')

opaque
  boxExtDep : {S : Shape} {B : Type â„“} {A : B â†’ âŸ¨ S âŸ© â†’ Type â„“'}
    {bâ‚€ bâ‚ : B} (b : bâ‚€ â‰¡ bâ‚)
    {r : âŸ¨ S âŸ©}
    {boxâ‚€ : OpenBox S (A bâ‚€) r} {boxâ‚ : OpenBox S (A bâ‚) r}
    â†’ boxâ‚€ .cof â‰¡ boxâ‚ .cof
    â†’ (âˆ€ i u v â†’ subst (Î» b' â†’ A b' i) b (boxâ‚€ .tube i u) â‰¡ boxâ‚ .tube i v)
    â†’ subst (A â¦…â€“â¦† r) b (boxâ‚€ .cap .out) â‰¡ boxâ‚ .cap .out
    â†’ subst (OpenBox S â¦…â€“â¦† r âˆ˜ A) b boxâ‚€ â‰¡ boxâ‚
  boxExtDep refl = boxExt

opaque
  fillerExt : {S : Shape} {A : âŸ¨ S âŸ© â†’ Type â„“}  {r : âŸ¨ S âŸ©}
    {box : OpenBox S A r}
    {fâ‚€ fâ‚ : Filler box}
    â†’ (âˆ€ s â†’ fâ‚€ .fill s .out â‰¡ fâ‚ .fill s .out)
    â†’ fâ‚€ â‰¡ fâ‚
  fillerExt p =
    congÎ£ makeFiller (funExt $ restrictExt âˆ˜ p) uip'

opaque
  FibStrExt : {A : Î“ â†’ Type â„“} {Î±â‚€ Î±â‚ : FibStr A}
    â†’ (âˆ€ S Î³ r box s â†’ Î±â‚€ .lift S Î³ r box .fill s .out â‰¡ Î±â‚ .lift S Î³ r box .fill s .out)
    â†’ Î±â‚€ â‰¡ Î±â‚
  FibStrExt q =
    congÎ£ makeFib
      (funExt' $ funExt' $ funExt' $ funExt' $ fillerExt $ q _ _ _ _)
      (funExt' $ funExt' $ funExt' $ funExt' $ funExt' $ funExt' $ funExt' uip')

------------------------------------------------------------------------------------------
-- Filling boxes expressed as partial elements in fibrations.
------------------------------------------------------------------------------------------

fibLiftPartial : (A : Î“ âŠ¢á¶ Type â„“')
  (S : Shape) (Î³ : Î“ ^ S) (r : âŸ¨ S âŸ©) (Ï† : Cof)
  (part : (s : âŸ¨ S âŸ©) â†’ [ Ï† âˆ¨ S âˆ‹ r â‰ˆ s ] â†’ A $á¶  Î³ s)
  â†’ (s : âŸ¨ S âŸ©) â†’ A $á¶  Î³ s [ Ï† âˆ¨ S âˆ‹ r â‰ˆ s â†¦ part s ]
fibLiftPartial (_ , Î±) S Î³ r Ï† part s .out =
  Î± .lift S Î³ r (partialToBox Ï† part) .fill s .out
fibLiftPartial (_ , Î±) S Î³ r Ï† part s .outâ‰¡ =
  âˆ¨-elimEq
    (Î± .lift S Î³ r (partialToBox Ï† part) .fill s .outâ‰¡)
    (Î» {refl â†’ sym (Î± .lift S Î³ r (partialToBox Ï† part) .capâ‰¡)})

fibVaryPartial : âˆ€ (A : Î“ âŠ¢á¶ Type â„“')
  {S T} (Ïƒ : Shape[ S , T ]) (Î³ : Î“ ^ T) (r : âŸ¨ S âŸ©) (Ï† : Cof)
  (part : (t : âŸ¨ T âŸ©) â†’ [ Ï† âˆ¨ T âˆ‹ âŸª Ïƒ âŸ« r â‰ˆ t ] â†’ A $á¶  Î³ t)
  (s : âŸ¨ S âŸ©)
  â†’ fibLiftPartial A T Î³ (âŸª Ïƒ âŸ« r) Ï† part (âŸª Ïƒ âŸ« s) .out
    â‰¡ fibLiftPartial A S (Î³ âˆ˜ âŸª Ïƒ âŸ«) r Ï† (reshapePartial Ïƒ part) s .out
fibVaryPartial (_ , Î±) Ïƒ Î³ r Ï† part s =
  Î± .vary _ _ Ïƒ Î³ r (partialToBox Ï† part) s
  âˆ™ cong
    (Î» box' â†’ Î± .lift _ (Î³ âˆ˜ âŸª Ïƒ âŸ«) r box' .fill s .out)
    (boxExt refl (Î» _ _ _ â†’ cong (part _) (cofIsStrictProp' (_ âˆ¨ _))) refl)

------------------------------------------------------------------------------------------
-- A strict retract of a fibration is a fibration.
------------------------------------------------------------------------------------------

RetractË£ : (A : Î“ â†’ Type â„“) (B : Î“ â†’ Type â„“') â†’ (Î“ â†’ Type (â„“ âŠ” â„“'))
RetractË£ A B Î³ = Retract (A Î³) (B Î³)

opaque
  retractFibStr : {A : Î“ â†’ Type â„“} {B : Î“ â†’ Type â„“'}
    â†’ Î“ âŠ¢Ë£ RetractË£ A B â†’ FibStr B â†’ FibStr A
  retractFibStr retract Î² .lift S Î³ r box = filler
    where
    fillerB : Filler (mapBox (retract _ .sec) box)
    fillerB = Î² .lift S Î³ r (mapBox (retract _ .sec) box)

    filler : Filler box
    filler .fill s .out = retract (Î³ s) .ret (fillerB .fill s .out)
    filler .fill s .outâ‰¡ u =
      sym (retract (Î³ s) .inv _)
      âˆ™ cong (retract (Î³ s) .ret) (fillerB .fill s .outâ‰¡ u)
    filler .capâ‰¡ =
      cong (retract (Î³ r) .ret) (fillerB .capâ‰¡)
      âˆ™ retract (Î³ r) .inv _

  retractFibStr retract Î² .vary S T Ïƒ Î³ r box s =
    cong (retract _ .ret) (Î² .vary S T Ïƒ Î³ r (mapBox (retract _ .sec) box) s)

opaque
  unfolding retractFibStr
  reindexRetractFibStr : {A : Î“ â†’ Type â„“} {B : Î“ â†’ Type â„“'}
    (retract : Î“ âŠ¢Ë£ RetractË£ A B) {Î² : FibStr B} (Ï : Î” â†’ Î“)
    â†’ retractFibStr retract Î² âˆ˜á¶ Ë¢ Ï  â‰¡ retractFibStr (retract âˆ˜ Ï) (Î² âˆ˜á¶ Ë¢ Ï)
  reindexRetractFibStr retract Ï = FibStrExt Î» _ _ _ _ _ â†’ refl

------------------------------------------------------------------------------------------
-- Corollary: fibration structures can be transferred across isomorphisms.
------------------------------------------------------------------------------------------

opaque
  isomorphFibStr : {A : Î“ â†’ Type â„“} {B : Î“ â†’ Type â„“'}
    â†’ Î“ âŠ¢Ë£ A â‰…Ë£ B â†’ FibStr B â†’ FibStr A
  isomorphFibStr iso Î² = retractFibStr (isoToRetract âˆ˜ iso) Î²

opaque
  unfolding isomorphFibStr
  reindexIsomorphFibStr : {A : Î“ â†’ Type â„“} {B : Î“ â†’ Type â„“'}
    (iso : Î“ âŠ¢Ë£ A â‰…Ë£ B) {Î² : FibStr B} (Ï : Î” â†’ Î“)
    â†’ isomorphFibStr iso Î² âˆ˜á¶ Ë¢ Ï â‰¡ isomorphFibStr (iso âˆ˜ Ï) (Î² âˆ˜á¶ Ë¢ Ï)
  reindexIsomorphFibStr _ = reindexRetractFibStr _
