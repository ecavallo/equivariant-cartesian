{-

Tinyness of shapes.

-}
module tiny where

open import basic
open import internal-extensional-type-theory
open import axiom.funext
open import axiom.shape
open import axiom.cofibration
open import axiom.tiny

infixr 4 _âˆšá´°_

module Tiny (@â™­ S : Shape) where

  open âˆšAxioms S public

  --â†“ One-sided naturality of left transposition is derivable.

  L^ : âˆ€ {@â™­ â„“ â„“' â„“''}
    {@â™­ A : Type â„“} {@â™­ A' : Type â„“'} {@â™­ B : Type â„“''}
    (@â™­ g : A' â†’ S âˆš B) (@â™­ h : A â†’ A')
    â†’ L g âˆ˜ (h `^ S) â‰¡ L (g âˆ˜ h)
  L^ g h = congâ™­ L (R^ h (L g))

  --â†“ Functoriality of âˆš in the type argument.

  âˆš` : âˆ€ {@â™­ â„“ â„“'}
    {@â™­ A : Type â„“} {@â™­ B : Type â„“'}
    (@â™­ h : A â†’ B) â†’ S âˆš A â†’ S âˆš B
  âˆš` h = R (h âˆ˜ L id)

  --â†“ The other naturality property of right transposition.

  âˆšR : âˆ€ {@â™­ â„“ â„“' â„“''}
    {@â™­ A : Type â„“} {@â™­ B : Type â„“'} {@â™­ C : Type â„“''}
    (@â™­ h : B â†’ C) (@â™­ f : (âŸ¨ S âŸ© â†’ A) â†’ B)
    â†’ âˆš` h âˆ˜ R f â‰¡ R (h âˆ˜ f)
  âˆšR {A = A} {B} {C = C} h f =
    sym (R^ (R f) (h âˆ˜ L id))
    âˆ™ congâ™­ (Î» f' â†’ R (h âˆ˜ f')) (L^ id (R f))

  --â†“ The other naturality property of left transposition.

  Lâˆš : âˆ€ {@â™­ â„“ â„“' â„“''}
    {@â™­ A : Type â„“} {@â™­ B : Type â„“'} {@â™­ C : Type â„“''}
    (@â™­ h : B â†’ C) (@â™­ g : A â†’ S âˆš B)
    â†’ h âˆ˜ L g  â‰¡ L (âˆš` h âˆ˜ g)
  Lâˆš h g = congâ™­ L (sym (âˆšR h (L g)))

--â†“ The right adjoint induces a dependent right adjoint
--â†“ TODO elaborate (including about universe level)

opaque
  _âˆšá´°_ : âˆ€ {@â™­ â„“ â„“'} (@â™­ S : Shape) {@â™­ Î“ : Type â„“}
    (@â™­ B : Î“ ^ S â†’ Type â„“')
    â†’ (Î“ â†’ Type (lsuc â„“'))
  (S âˆšá´° B) Î³ = Î£ C âˆˆ S âˆš (Type* _) , âˆš` fst C â‰¡ R B Î³
    where
    open Tiny S

module DependentTiny (@â™­ S : Shape) where

  open Tiny S

  module _ {@â™­ â„“ â„“'} {@â™­ Î“ : Type â„“} (@â™­ B : Î“ ^ S â†’ Type â„“') where

    opaque
      unfolding _âˆšá´°_

      Rá´° : @â™­ (Î“ ^ S âŠ¢Ë£ B) â†’ (Î“ âŠ¢Ë£ S âˆšá´° B)
      Rá´° f Î³ .fst = R (B ,, f) Î³
      Rá´° f _ .snd = cong$ (âˆšR fst (B ,, f))

      Lá´° : @â™­ (Î“ âŠ¢Ë£ S âˆšá´° B) â†’ (Î“ ^ S âŠ¢Ë£ B)
      Lá´° g p =
        coe
          (cong$ (Lâˆš fst (fst âˆ˜ g) âˆ™ congâ™­ L (funExt (snd âˆ˜ g))))
          (L (fst âˆ˜ g) p .snd)

      LRá´° : (@â™­ f : Î“ ^ S âŠ¢Ë£ B) â†’ Lá´° (Rá´° f) â‰¡ f
      LRá´° f =
        funExt' $ adjustSubstEq id refl refl _ refl refl

      RLá´° : (@â™­ g : Î“ âŠ¢Ë£ S âˆšá´° B) â†’ Rá´° (Lá´° g) â‰¡ g
      RLá´° g =
        funExt' $ Î£ext (cong$ (congâ™­ R (sym lemma))) uip'
        where
        lemma : L (fst âˆ˜ g) â‰¡ (B ,, Lá´° g)
        lemma = funExt' $ Î£ext _ refl

  opaque
    unfolding Rá´° Lá´°

    âˆšá´°-reindex : âˆ€ {@â™­ â„“ â„“' â„“''} {@â™­ Î“ : Type â„“} {@â™­ Î“' : Type â„“'}
      (@â™­ Ï : Î“ â†’ Î“')
      (@â™­ B : Î“' ^ S â†’ Type â„“'')
      â†’ âˆ€ Î³ â†’ (S âˆšá´° B) (Ï Î³) â‰¡ (S âˆšá´° B âˆ˜ (Ï `^ S)) Î³
    âˆšá´°-reindex Ï B _ =
      cong (Î» T â†’ Î£ C âˆˆ S âˆš (Type* _) , âˆš` fst C â‰¡ T) (cong$ (sym (R^ Ï B)))

  âˆšá´°-reindex-compute : âˆ€ {@â™­ â„“ â„“' â„“''} {@â™­ Î“ : Type â„“} {@â™­ Î“' : Type â„“'}
    (@â™­ Ï : Î“ â†’ Î“')
    {@â™­ B : Î“' ^ S â†’ Type â„“''}
    â†’ Î“ âŠ¢Ë£ (S âˆšá´° B) âˆ˜ Ï â†’Ë£ S âˆšá´° (B âˆ˜ (Ï `^ S))
  âˆšá´°-reindex-compute Ï {B = B} Î³ = coe (âˆšá´°-reindex Ï B Î³)

  âˆšá´°-reindex-expand : âˆ€ {@â™­ â„“ â„“' â„“''} {@â™­ Î“ : Type â„“} {@â™­ Î“' : Type â„“'}
    (@â™­ Ï : Î“ â†’ Î“')
    {@â™­ B : Î“' ^ S â†’ Type â„“''}
    â†’ Î“ âŠ¢Ë£ S âˆšá´° (B âˆ˜ (Ï `^ S)) â†’Ë£ (S âˆšá´° B) âˆ˜ Ï
  âˆšá´°-reindex-expand Ï {B = B} Î³ = coe (sym (âˆšá´°-reindex Ï B Î³))

  âˆšá´°-reindex-compute-âˆ˜ : âˆ€ {@â™­ â„“ â„“' â„“'' â„“'''}
    {@â™­ Î“ : Type â„“} {@â™­ Î“' : Type â„“'} {@â™­ Î“'' : Type â„“''}
    (@â™­ Ï' : Î“' â†’ Î“'') (@â™­ Ï : Î“ â†’ Î“')
    {@â™­ B : Î“'' ^ S â†’ Type â„“'''}
    (b : Î“ âŠ¢Ë£ (S âˆšá´° B) âˆ˜ Ï' âˆ˜ Ï)
    â†’ appË£ (âˆšá´°-reindex-compute Ï) (appË£ (âˆšá´°-reindex-compute Ï' âˆ˜ Ï) b)
      â‰¡ appË£ (âˆšá´°-reindex-compute (Ï' âˆ˜ Ï)) b
  âˆšá´°-reindex-compute-âˆ˜ Ï' Ï {B = B} b =
    funExt Î» Î³ â†’
    adjustSubstEq
      id
      refl
      (âˆšá´°-reindex Ï' B (Ï Î³))
      (âˆšá´°-reindex Ï (B âˆ˜ (Ï' `^ S)) Î³)
      (âˆšá´°-reindex (Ï' âˆ˜ Ï) B Î³)
      refl

  âˆšá´°-reindex-expand-âˆ˜ : âˆ€ {@â™­ â„“ â„“' â„“'' â„“'''}
    {@â™­ Î“ : Type â„“} {@â™­ Î“' : Type â„“'} {@â™­ Î“'' : Type â„“''}
    (@â™­ Ï' : Î“' â†’ Î“'') (@â™­ Ï : Î“ â†’ Î“')
    {@â™­ B : Î“'' ^ S â†’ Type â„“'''}
    (b : Î“ âŠ¢Ë£ S âˆšá´° (B âˆ˜ (Ï' âˆ˜ Ï) `^ S))
    â†’ appË£ (âˆšá´°-reindex-expand Ï' âˆ˜ Ï) (appË£ (âˆšá´°-reindex-expand Ï) b)
      â‰¡ appË£ (âˆšá´°-reindex-expand (Ï' âˆ˜ Ï)) b
  âˆšá´°-reindex-expand-âˆ˜ Ï' Ï {B = B} b =
    funExt Î» Î³ â†’
    adjustSubstEq
      id
      refl
      (sym (âˆšá´°-reindex Ï (B âˆ˜ (Ï' `^ S)) Î³))
      (sym (âˆšá´°-reindex Ï' B (Ï Î³)))
      (sym (âˆšá´°-reindex (Ï' âˆ˜ Ï) B Î³))
      refl

  âˆšá´°-reindex-compute-expand : âˆ€ {@â™­ â„“ â„“' â„“''} {@â™­ Î“ : Type â„“} {@â™­ Î“' : Type â„“'}
    (@â™­ Ï : Î“ â†’ Î“')
    {@â™­ B : Î“' ^ S â†’ Type â„“''}
    (b : Î“ âŠ¢Ë£ S âˆšá´° (B âˆ˜ (Ï `^ S)))
    â†’ appË£ (âˆšá´°-reindex-compute Ï {B}) (appË£ (âˆšá´°-reindex-expand Ï) b) â‰¡ b
  âˆšá´°-reindex-compute-expand Ï {B} b =
    funExt Î» Î³ â†’ adjustSubstEq id refl _ (âˆšá´°-reindex Ï B Î³) refl refl

  âˆšá´°-reindex-expand-compute : âˆ€ {@â™­ â„“ â„“' â„“''} {@â™­ Î“ : Type â„“} {@â™­ Î“' : Type â„“'}
    (@â™­ Ï : Î“ â†’ Î“')
    {@â™­ B : Î“' ^ S â†’ Type â„“''}
    (b : Î“ âŠ¢Ë£ (S âˆšá´° B) âˆ˜ Ï)
    â†’ appË£ (âˆšá´°-reindex-expand Ï {B}) (appË£ (âˆšá´°-reindex-compute Ï) b) â‰¡ b
  âˆšá´°-reindex-expand-compute Ï {B} b =
    funExt Î» Î³ â†’ adjustSubstEq id refl _ (sym (âˆšá´°-reindex Ï B Î³)) refl refl

  opaque
    unfolding Rá´° Lá´° âˆšá´°-reindex

    R^á´° : âˆ€ {@â™­ â„“ â„“' â„“''}
      {@â™­ Î“ : Type â„“} {@â™­ Î“' : Type â„“'}
      {@â™­ B : Î“' ^ S â†’ Type â„“''}
      (@â™­ Ï : Î“ â†’ Î“')
      (@â™­ f : Î“' ^ S âŠ¢Ë£ B)
      â†’ appË£ (âˆšá´°-reindex-compute Ï) (Rá´° B f âˆ˜ Ï) â‰¡ Rá´° (B âˆ˜ (Ï `^ S)) (f âˆ˜ (Ï `^ S))
    R^á´° {B = B} Ï f =
      funExt Î» Î³ â†’
      sym (substCongAssoc id (Î» T â†’ Î£ C âˆˆ _ , âˆš` fst C â‰¡ T) (cong$ (sym (R^ Ï B))) _)
      âˆ™ Î£ext
        (substNaturality fst (cong$ (sym (R^ Ï B)))
          âˆ™ substConst (cong$ (sym (R^ Ï B))) _
          âˆ™ cong$ (sym (R^ Ï (B ,, f))))
        uip'

  opaque
    unfolding âˆšá´°-reindex

    L^á´° : âˆ€ {@â™­ â„“ â„“' â„“''}
      {@â™­ Î“ : Type â„“} {@â™­ Î“' : Type â„“'}
      {@â™­ B : Î“' ^ S â†’ Type â„“''}
      (@â™­ g : Î“' âŠ¢Ë£ S âˆšá´° B)
      (@â™­ Ï : Î“ â†’ Î“')
      â†’ Lá´° B g âˆ˜ (Ï `^ S) â‰¡ Lá´° (B âˆ˜ (Ï `^ S)) (appË£ (âˆšá´°-reindex-compute Ï) (g âˆ˜ Ï))
    L^á´° {B = B} g Ï =
      sym (LRá´° (B âˆ˜ (Ï `^ S)) (Lá´° B g âˆ˜ (Ï `^ S)))
      âˆ™ congâ™­ (Lá´° (B âˆ˜ (Ï `^ S)))
        (sym (R^á´° Ï (Lá´° B g)) âˆ™ cong (appË£ (âˆšá´°-reindex-compute Ï)) (cong (_âˆ˜ Ï) (RLá´° B g)))

  opaque
    iná´° : âˆ€ {@â™­ â„“ â„“'} {@â™­ Î“ : Type â„“} {@â™­ B : Î“ ^ S â†’ Type â„“'}
      â†’ @â™­ (Î“ ^ S âŠ¢Ë£ B)
      â†’ (Î“ âŠ¢Ë£ S âˆšá´° B)
    iná´° = Rá´° _

    outá´° : âˆ€ {@â™­ â„“ â„“'} {@â™­ Î“ : Type â„“} {@â™­ B : Î“ â–·âŸ¨ S âŸ© ^ S â†’ Type â„“'}
      â†’ @â™­ (Î“ â–·âŸ¨ S âŸ© âŠ¢Ë£ S âˆšá´° B)
      â†’ Î“ âŠ¢Ë£ B âˆ˜ ^-Î· S
    outá´° t = Lá´° _ t âˆ˜ ^-Î· S

    out-iná´° : âˆ€ {@â™­ â„“ â„“'} {@â™­ Î“ : Type â„“} {@â™­ B : (Î“ â–·âŸ¨ S âŸ©) ^ S â†’ Type â„“'}
      (@â™­ b : Î“ â–·âŸ¨ S âŸ© ^ S âŠ¢Ë£ B)
      â†’ outá´° (iná´° b) â‰¡ b âˆ˜ ^-Î· S
    out-iná´° b = cong (_âˆ˜ ^-Î· S) (LRá´° _ b)

    in-outá´° : âˆ€ {@â™­ â„“ â„“'} {@â™­ Î“ : Type â„“} {@â™­ B : Î“ ^ S â†’ Type â„“'}
      (@â™­ t : Î“ âŠ¢Ë£ S âˆšá´° B)
      â†’ t â‰¡ iná´° (outá´° (appË£ (âˆšá´°-reindex-compute (^-Îµ S)) (t âˆ˜ ^-Îµ S)))
    in-outá´° t =
      sym (RLá´° _ t) âˆ™ congâ™­ (Rá´° _) (cong (_âˆ˜ ^-Î· S) (L^á´° t (^-Îµ S)))

    iná´°-reindex : âˆ€ {@â™­ â„“ â„“' â„“''}
        {@â™­ Î“ : Type â„“} {@â™­ Î“' : Type â„“'}
        {@â™­ B : Î“' ^ S â†’ Type â„“''}
        (@â™­ Ï : Î“ â†’ Î“')
        (@â™­ f : Î“' ^ S âŠ¢Ë£ B)
        â†’ appË£ (âˆšá´°-reindex-compute Ï) (iná´° f âˆ˜ Ï) â‰¡ iná´° (f âˆ˜ (Ï `^ S))
    iná´°-reindex = R^á´°

    outá´°-reindex : âˆ€ {@â™­ â„“ â„“' â„“''}
      {@â™­ Î“ : Type â„“} {@â™­ Î“' : Type â„“'}
      {@â™­ B : Î“' â–·âŸ¨ S âŸ© ^ S â†’ Type â„“''}
      (@â™­ Ï : Î“ â†’ Î“')
      (@â™­ t : Î“' â–·âŸ¨ S âŸ© âŠ¢Ë£ S âˆšá´° B)
      â†’ outá´° t âˆ˜ Ï â‰¡ outá´° (appË£ (âˆšá´°-reindex-compute (Ï Ã—id)) (t âˆ˜ Ï Ã—id))
    outá´°-reindex Ï t =
      cong (_âˆ˜ ^-Î· S) (L^á´° t (Ï Ã—id))

  opaque
    âˆšá´°` : âˆ€ {@â™­ â„“ â„“' â„“''} {@â™­ Î“ : Type â„“}
      {@â™­ B : Î“ ^ S â†’ Type â„“'} {@â™­ B' : Î“ ^ S â†’ Type â„“''}
      (@â™­ h : Î“ ^ S âŠ¢Ë£ B â†’Ë£ B')
      â†’ @â™­ (Î“ âŠ¢Ë£ S âˆšá´° B)
      â†’ Î“ âŠ¢Ë£ S âˆšá´° B'
    âˆšá´°` h t =
      iná´° $â™­
      appË£ h $
      outá´° $â™­
      appË£ (âˆšá´°-reindex-compute (^-Îµ S)) $
      t âˆ˜ ^-Îµ S

    âˆš-iná´° : âˆ€ {@â™­ â„“ â„“' â„“''} {@â™­ Î“ : Type â„“}
        {@â™­ B : Î“ ^ S â†’ Type â„“'} {@â™­ B' : Î“ ^ S â†’ Type â„“''}
        (@â™­ h : Î“ ^ S âŠ¢Ë£ B â†’Ë£ B')
        (@â™­ b : Î“ ^ S âŠ¢Ë£ B)
        â†’ âˆšá´°` h (iná´° b) â‰¡ iná´° (appË£ h b)
    âˆš-iná´° h b =
      congâ™­ (Î» t â†’ iná´° $â™­ appË£ h $ outá´° $â™­ t) (iná´°-reindex _ b)
      âˆ™ congâ™­ (Î» b' â†’ iná´° $â™­ appË£ h b') (out-iná´° _)

    out-âˆšá´° : âˆ€ {@â™­ â„“ â„“' â„“''} {@â™­ Î“ : Type â„“}
        {@â™­ B : Î“ â–·âŸ¨ S âŸ© ^ S â†’ Type â„“'} {@â™­ B' : Î“ â–·âŸ¨ S âŸ© ^ S â†’ Type â„“''}
        (@â™­ h : Î“ â–·âŸ¨ S âŸ© ^ S âŠ¢Ë£ B â†’Ë£ B')
        (@â™­ t : Î“ â–·âŸ¨ S âŸ© âŠ¢Ë£ S âˆšá´° B)
        â†’ outá´° (âˆšá´°` h t) â‰¡ appË£ (h âˆ˜ ^-Î· S) (outá´° t)
    out-âˆšá´° h t =
      out-iná´° _
      âˆ™ cong (appË£ (h âˆ˜ ^-Î· S))
          (outá´°-reindex (^-Î· S) _
            âˆ™ congâ™­ (outá´°) (âˆšá´°-reindex-compute-âˆ˜ (^-Îµ S) (^-Î· S Ã—id) t)
            âˆ™ sym (outá´°-reindex _ t))

  opaque
    âˆšá´°PreservesProp : âˆ€ {@â™­ â„“ â„“'} {@â™­ Î“ : Type â„“}
      (@â™­ B : Î“ ^ S â†’ Type â„“')
      â†’ @â™­ ((@â™­ b b' : Î“ ^ S âŠ¢Ë£ B) â†’ b â‰¡ b')
      â†’ ((@â™­ t t' : Î“ âŠ¢Ë£ S âˆšá´° B) â†’ t â‰¡ t')
    âˆšá´°PreservesProp B propB t t' =
      in-outá´° t âˆ™ congâ™­ iná´° (propB _ _) âˆ™ sym (in-outá´° t')

    âˆšá´°PreservesProp' : âˆ€ {@â™­ â„“ â„“'} {@â™­ Î“ : Type â„“}
      (@â™­ B : Î“ ^ S â†’ Type â„“')
      â†’ @â™­ (âˆ€ p (b b' : B p) â†’ b â‰¡ b')
      â†’ âˆ€ Î³ (t t' : (S âˆšá´° B) Î³) â†’ t â‰¡ t'
    âˆšá´°PreservesProp' {Î“ = Î“} B propB Î³ t t' =
      cong$ {a = Î³ , (t , t')} lem''
      where
      lem : ((@â™­ t t' : Î“ â–·Ë£ (S âˆšá´° B Ã—Ë£ S âˆšá´° B) âŠ¢Ë£ S âˆšá´° (B âˆ˜ (ğ’‘ `^ S))) â†’ t â‰¡ t')
      lem =
        âˆšá´°PreservesProp
          {Î“ = Î“ â–·Ë£ (S âˆšá´° B Ã—Ë£ S âˆšá´° B)}
          (B âˆ˜ (ğ’‘ `^ S))
          (Î» b b' â†’ funExt Î» p â†’ propB (ğ’‘ âˆ˜ p) (b p) (b' p))

      lem' :
        _â‰¡_
          {A = Î“ â–·Ë£ (S âˆšá´° B Ã—Ë£ S âˆšá´° B) âŠ¢Ë£ S âˆšá´° (B âˆ˜ (ğ’‘ `^ S))}
          (appË£ (âˆšá´°-reindex-compute ğ’‘) (fstË£ ğ’’))
          (appË£ (âˆšá´°-reindex-compute ğ’‘) (sndË£ ğ’’))
      lem' =
        lem (appË£ (âˆšá´°-reindex-compute ğ’‘) (fstË£ ğ’’)) (appË£ (âˆšá´°-reindex-compute ğ’‘) (sndË£ ğ’’))

      lem'' :
        _â‰¡_
          {A = Î“ â–·Ë£ (S âˆšá´° B Ã—Ë£ S âˆšá´° B) âŠ¢Ë£ (S âˆšá´° B) âˆ˜ ğ’‘}
          (fstË£ ğ’’)
          (sndË£ ğ’’)
      lem'' =
        sym (âˆšá´°-reindex-expand-compute ğ’‘ (fstË£ ğ’’))
        âˆ™ cong (appË£ (âˆšá´°-reindex-expand ğ’‘)) lem'
        âˆ™ âˆšá´°-reindex-expand-compute ğ’‘ (sndË£ ğ’’)
