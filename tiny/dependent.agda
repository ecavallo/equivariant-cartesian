{-

The right adjoint âˆš to exponentation by a shape extends (with a caveat) to a dependent right
adjoint (DRA) âˆšá´° in the sense of

[1] Birkedal, Clouston, Mannaa, MÃ¸gelberg, Pitts, & Spitters.
    Modal dependent type theory and dependent right adjoints.
    https://doi.org/10.1017/S0960129519000197

This is convenient for defining the universe of fibrations (especially with the added
complication of the equivariance condition), as observed in passing in

[2] Licata, Orton, Pitts, & Spitters.
    Internal Universes in Models of Homotopy Type Theory.
    https://doi.org/10.4230/LIPIcs.FSCD.2018.22

The caveat concerns universe level: the definition of âˆšá´° uses a universe ğ‘½, and âˆšá´° then
takes ğ‘½-small types to types in the *next* universe. Compare the construction in Â§4 of
[1], where local universes are used to construct a CwF with a DRA.

In the motivating cubical set semantics, there is an direct construction of this DRA
which does not raise universe level. Namely, given a type family Î“Ë¢.A â†’ Î“Ë¢ we apply the
right adjoint âˆš and reindex along the unit Î“ â†’ âˆš(Î“Ë¢) to define a family Î“.âˆšB â†’ Î“.

Î“.âˆšB â†’ âˆš(Î“Ë¢.B)
 | âŒŸ     |
 â†“       â†“
 Î“ â€”â€”â€”â†’ âˆš(Î“Ë¢)

However, in our internal setting we do not know that âˆš preserves maps with ğ‘½-small fibers.

Using the fact that exponentiation by a shape has a further left adjoint (namely product
with that shape), we formulate the elimination rule in the style of

[3] Gratzer, Cavallo, Kavvos, Guatto, & Birkedal.
    Modalities and parametric adjoints.
    https://doi.org/10.1145/3514241

-}
module tiny.dependent where

open import basic
open import internal-extensional-type-theory
open import axiom.funext
open import axiom.shape
open import axiom.cofibration
open import axiom.tiny
open import tiny.basic

infixr 5 _âˆšá´°_


--â†“ Definition of the dependent right adjoint, which takes a family B over Î“ ^ S and
--â†“ produces a family S âˆšá´° B over Î“, with the intention that we have a natural isomorphism
--â†“ between sections of Î“ ^ S âŠ¢ B and sections of Î“ âŠ¢ S âˆšá´° B.

opaque
  _âˆšá´°_ : âˆ€ {@â™­ â„“ â„“'} (@â™­ S : Shape) {@â™­ Î“ : Type â„“}
    (@â™­ B : Î“ ^ S â†’ Type â„“')
    â†’ (Î“ â†’ Type (lsuc â„“'))
  _âˆšá´°_ {â„“' = â„“'} S B Î³ =
    Î£ C âˆˆ S âˆš (Type* â„“') , âˆš` fst C â‰¡ transposeRight B Î³
    where
    open Tiny S

module DependentTiny (@â™­ S : Shape) where

  open Tiny S

  opaque
    unfolding _âˆšá´°_

    --â†“ The operator âˆšá´° commutes with reindexing. Unfortunately this equality is not
    --â†“ definition, which leads to some bureaucratic complications later on.

    reindexâˆš : âˆ€ {@â™­ â„“ â„“' â„“''} {@â™­ Î“ : Type â„“} {@â™­ Î“' : Type â„“'}
      (@â™­ B : Î“' ^ S â†’ Type â„“'')
      (@â™­ Ï : Î“ â†’ Î“')
      â†’ âˆ€ Î³ â†’ ((S âˆšá´° B) âˆ˜ Ï) Î³ â‰¡ (S âˆšá´° (B âˆ˜ (Ï `^ S))) Î³
    reindexâˆš B Ï _ =
      cong
        (Î» T â†’ Î£ C âˆˆ S âˆš (Type* _) , âˆš` fst C â‰¡ T)
        (cong$ (sym (transposeRight^ Ï B)))

  --â†“ Helper functions for shifting substitutions in and out of âˆšá´°; these are just
  --â†“ instances of coercion along the equality proven above.

  module _ {@â™­ â„“ â„“' â„“''} {@â™­ Î“ : Type â„“} {@â™­ Î“' : Type â„“'}
    {@â™­ B : Î“' ^ S â†’ Type â„“''} (@â™­ Ï : Î“ â†’ Î“')
    where

    doReindexâˆš : Î“ âŠ¢Ë£ (S âˆšá´° B) âˆ˜ Ï â†’Ë£ S âˆšá´° (B âˆ˜ (Ï `^ S))
    doReindexâˆš Î³ = coe (reindexâˆš B Ï Î³)

    undoReindexâˆš : Î“ âŠ¢Ë£ S âˆšá´° (B âˆ˜ (Ï `^ S)) â†’Ë£ (S âˆšá´° B) âˆ˜ Ï
    undoReindexâˆš Î³ = coe (sym (reindexâˆš B Ï Î³))

    doUndoReindexâˆš : (b : Î“ âŠ¢Ë£ S âˆšá´° (B âˆ˜ (Ï `^ S)))
      â†’ appË£ doReindexâˆš (appË£ undoReindexâˆš b) â‰¡ b
    doUndoReindexâˆš b =
      funExt Î» Î³ â†’ adjustSubstEq id refl _ (reindexâˆš B Ï Î³) refl refl

    undoDoReindexâˆš : (b : Î“ âŠ¢Ë£ (S âˆšá´° B) âˆ˜ Ï)
      â†’ appË£ undoReindexâˆš (appË£ doReindexâˆš b) â‰¡ b
    undoDoReindexâˆš b =
      funExt Î» Î³ â†’ adjustSubstEq id refl _ (sym (reindexâˆš B Ï Î³)) refl refl

  module _ {@â™­ â„“ â„“' â„“'' â„“'''}
    {@â™­ Î“ : Type â„“} {@â™­ Î“' : Type â„“'} {@â™­ Î“'' : Type â„“''}
    {@â™­ B : Î“'' ^ S â†’ Type â„“'''}
    (@â™­ Ï' : Î“' â†’ Î“'') (@â™­ Ï : Î“ â†’ Î“')
    where

    doReindexâˆš-âˆ˜ : (b : Î“ âŠ¢Ë£ (S âˆšá´° B) âˆ˜ Ï' âˆ˜ Ï)
      â†’ appË£ (doReindexâˆš Ï) (appË£ (doReindexâˆš Ï' âˆ˜ Ï) b)
        â‰¡ appË£ (doReindexâˆš (Ï' âˆ˜ Ï)) b
    doReindexâˆš-âˆ˜ b =
      funExt Î» Î³ â†’
      adjustSubstEq id
        refl
        (reindexâˆš B Ï' (Ï Î³))
        (reindexâˆš (B âˆ˜ (Ï' `^ S)) Ï Î³)
        (reindexâˆš B (Ï' âˆ˜ Ï) Î³)
        refl

    undoReindexâˆš-âˆ˜ : (b : Î“ âŠ¢Ë£ S âˆšá´° (B âˆ˜ (Ï' âˆ˜ Ï) `^ S))
      â†’ appË£ (undoReindexâˆš Ï' âˆ˜ Ï) (appË£ (undoReindexâˆš Ï) b)
        â‰¡ appË£ (undoReindexâˆš (Ï' âˆ˜ Ï)) b
    undoReindexâˆš-âˆ˜ b =
      funExt Î» Î³ â†’
      adjustSubstEq id
        refl
        (sym (reindexâˆš (B âˆ˜ (Ï' `^ S)) Ï Î³))
        (sym (reindexâˆš B Ï' (Ï Î³)))
        (sym (reindexâˆš B (Ï' âˆ˜ Ï) Î³))
        refl

  module _ {@â™­ â„“ â„“'} {@â™­ Î“ : Type â„“} {@â™­ B : Î“ ^ S â†’ Type â„“'} where

    opaque
      unfolding _âˆšá´°_

      --â†“ Introduction rule for âˆšá´°, corresponding to left-to-right transposition along the
      --â†“ adjunction.

      shutâˆš : @â™­ (Î“ ^ S âŠ¢Ë£ B) â†’ (Î“ âŠ¢Ë£ S âˆšá´° B)
      shutâˆš b Î³ .fst = transposeRight (B ,, b) Î³
      shutâˆš b Î³ .snd = cong$ (âˆšTransposeRight fst (B ,, b))

      --â†“ Inverse to the introduction rule, corresopnding to right-to-left transposition.

      unshutâˆš : @â™­ (Î“ âŠ¢Ë£ S âˆšá´° B) â†’ (Î“ ^ S âŠ¢Ë£ B)
      unshutâˆš t Î³ =
        coe
          (cong$ (âˆšTransposeLeft fst (fst âˆ˜ t) âˆ™ congâ™­ transposeLeft (funExt (snd âˆ˜ t))))
          (transposeLeft (fst âˆ˜ t) Î³ .snd)

      --â†“ Inverse laws.

      unshutShutâˆš : (@â™­ b : Î“ ^ S âŠ¢Ë£ B) â†’ unshutâˆš (shutâˆš b) â‰¡ b
      unshutShutâˆš b =
        funExt' $ adjustSubstEq id refl refl _ refl refl

      shutUnshutâˆš : (@â™­ t : Î“ âŠ¢Ë£ S âˆšá´° B) â†’ shutâˆš (unshutâˆš t) â‰¡ t
      shutUnshutâˆš t =
        funExt' $ Î£ext (cong$ (congâ™­ transposeRight (sym lemma))) uip'
        where
        lemma : transposeLeft (fst âˆ˜ t) â‰¡ (B ,, unshutâˆš t)
        lemma = funExt' $ Î£ext _ refl

  --â†“ Elimination rule for âˆšá´°. We prefer this to unshutâˆš because it lands in an arbitrary
  --â†“ context Î“ and thus has better properties with respect to reindexing.

  openâˆš : âˆ€ {@â™­ â„“ â„“'} {@â™­ Î“ : Type â„“} {@â™­ B : Î“ â–·âŸ¨ S âŸ© ^ S â†’ Type â„“'}
    â†’ @â™­ (Î“ â–·âŸ¨ S âŸ© âŠ¢Ë£ S âˆšá´° B)
    â†’ Î“ âŠ¢Ë£ B âˆ˜ ^-unit S
  openâˆš t = unshutâˆš t âˆ˜ ^-unit S

  module _ {@â™­ â„“ â„“' â„“''} {@â™­ Î“ : Type â„“} {@â™­ Î“' : Type â„“'} {@â™­ B : Î“' ^ S â†’ Type â„“''}
    where

    opaque
      unfolding reindexâˆš shutâˆš

      --â†“ The introduction rule commutes with reindexing.

      reindexShutâˆš : (@â™­ b : Î“' ^ S âŠ¢Ë£ B) (@â™­ Ï : Î“ â†’ Î“')
        â†’ appË£ (doReindexâˆš Ï) (shutâˆš b âˆ˜ Ï) â‰¡ shutâˆš (b âˆ˜ (Ï `^ S))
      reindexShutâˆš b Ï =
        funExt Î» Î³ â†’
        sym
          (substCongAssoc
            id
            (Î» T â†’ Î£ C âˆˆ _ , âˆš` fst C â‰¡ T)
            (cong$ (sym (transposeRight^ Ï B))) _)
        âˆ™ Î£ext
          (substNaturality fst (cong$ (sym (transposeRight^ Ï B)))
            âˆ™ substConst (cong$ (sym (transposeRight^ Ï B))) _
            âˆ™ cong$ (sym (transposeRight^ Ï (B ,, b))))
          uip'

    --â†“ The inverse to the introduction rule commutes with reindexing along substitutions
    --â†“ of the form (Ï `^ S).

    opaque
      reindexUnshutâˆš : (@â™­ g : Î“' âŠ¢Ë£ S âˆšá´° B) (@â™­ Ï : Î“ â†’ Î“')
        â†’ unshutâˆš g âˆ˜ (Ï `^ S) â‰¡ unshutâˆš (appË£ (doReindexâˆš Ï) (g âˆ˜ Ï))
      reindexUnshutâˆš g Ï =
        sym (unshutShutâˆš (unshutâˆš g âˆ˜ (Ï `^ S)))
        âˆ™ congâ™­ unshutâˆš
          (sym (reindexShutâˆš (unshutâˆš g) Ï)
            âˆ™ cong (appË£ (doReindexâˆš Ï)) (cong (_âˆ˜ Ï) (shutUnshutâˆš g)))

  --â†“ The elimination rule commutes with reindexing.

  reindexOpenâˆš : âˆ€ {@â™­ â„“ â„“' â„“''}
    {@â™­ Î“ : Type â„“} {@â™­ Î“' : Type â„“'}
    {@â™­ B : Î“' â–·âŸ¨ S âŸ© ^ S â†’ Type â„“''}
    (@â™­ Ï : Î“ â†’ Î“')
    (@â™­ t : Î“' â–·âŸ¨ S âŸ© âŠ¢Ë£ S âˆšá´° B)
    â†’ openâˆš t âˆ˜ Ï â‰¡ openâˆš (appË£ (doReindexâˆš (Ï Ã—id)) (t âˆ˜ Ï Ã—id))
  reindexOpenâˆš Ï t =
    cong (_âˆ˜ ^-unit S) (reindexUnshutâˆš t (Ï Ã—id))

  opaque
    --â†“ Computation (Î²) rule.

    openShutâˆš : âˆ€ {@â™­ â„“ â„“'} {@â™­ Î“ : Type â„“} {@â™­ B : (Î“ â–·âŸ¨ S âŸ©) ^ S â†’ Type â„“'}
      (@â™­ b : Î“ â–·âŸ¨ S âŸ© ^ S âŠ¢Ë£ B)
      â†’ openâˆš (shutâˆš b) â‰¡ b âˆ˜ ^-unit S
    openShutâˆš b = cong (_âˆ˜ ^-unit S) (unshutShutâˆš b)

    --â†“ Uniqueness (Î·) rule.

    shutOpenâˆš : âˆ€ {@â™­ â„“ â„“'} {@â™­ Î“ : Type â„“} {@â™­ B : Î“ ^ S â†’ Type â„“'}
      (@â™­ t : Î“ âŠ¢Ë£ S âˆšá´° B)
      â†’ t â‰¡ shutâˆš (openâˆš (appË£ (doReindexâˆš (^-counit S)) (t âˆ˜ ^-counit S)))
    shutOpenâˆš t =
      sym (shutUnshutâˆš t)
      âˆ™ congâ™­ shutâˆš (cong (_âˆ˜ ^-unit S) (reindexUnshutâˆš t (^-counit S)))

  opaque
    --â†“ âˆšá´° preserves propositions (with respect to strict equality). First we prove that
    --â†“ if the type of sections of Î“ ^ S âŠ¢Ë£ B is a proposition, then so is the type of
    --â†“ sections of Î“ âŠ¢Ë£ S âˆšá´° B . Then we use this to deduce that if all fibers of B are
    --â†“ propositions, then so are all fibers of S âˆšá´° B.

    âˆšá´°PreservesPropSections : âˆ€ {@â™­ â„“ â„“'} {@â™­ Î“ : Type â„“}
      (@â™­ B : Î“ ^ S â†’ Type â„“')
      â†’ @â™­ ((@â™­ b b' : Î“ ^ S âŠ¢Ë£ B) â†’ b â‰¡ b')
      â†’ ((@â™­ t t' : Î“ âŠ¢Ë£ S âˆšá´° B) â†’ t â‰¡ t')
    âˆšá´°PreservesPropSections B propB t t' =
      shutOpenâˆš t âˆ™ congâ™­ shutâˆš (propB _ _) âˆ™ sym (shutOpenâˆš t')

    âˆšá´°PreservesProp : âˆ€ {@â™­ â„“ â„“'} {@â™­ Î“ : Type â„“}
      (@â™­ B : Î“ ^ S â†’ Type â„“')
      â†’ @â™­ (âˆ€ Î³ â†’ isStrictProp (B Î³))
      â†’ âˆ€ Î³ â†’ isStrictProp ((S âˆšá´° B) Î³)
    âˆšá´°PreservesProp {Î“ = Î“} B propB Î³ t t' =
      cong$ {a = Î³ , (t , t')} equateGenericPoints
      where
      equateGenericPoints : Î“ â–·Ë£ (S âˆšá´° B Ã—Ë£ S âˆšá´° B) âŠ¢Ë£ fstË£ ğ’’ â‰¡ sndË£ ğ’’ â¦‚ (S âˆšá´° B) âˆ˜ ğ’‘
      equateGenericPoints =
        sym (undoDoReindexâˆš ğ’‘ (fstË£ ğ’’))
        âˆ™ cong (appË£ (undoReindexâˆš ğ’‘))
          (âˆšá´°PreservesPropSections
            (B âˆ˜ (ğ’‘ `^ S))
            (Î» b b' â†’ funExt Î» Î³' â†’ propB (ğ’‘ âˆ˜ Î³') (b Î³') (b' Î³'))
            (appË£ (doReindexâˆš ğ’‘) (fstË£ ğ’’))
            (appË£ (doReindexâˆš ğ’‘) (sndË£ ğ’’)))
        âˆ™ undoDoReindexâˆš ğ’‘ (sndË£ ğ’’)
