{-

Definition of the universe of propositional cofibrations and basic
operations involving these.

-}
{-# OPTIONS --rewriting #-}

module axioms.cofprop where

open import prelude
open import axioms.funext
open import axioms.truncation
open import axioms.shape

private variable РёЊ РёЊ' : Level

infixr 4 _Рѕе_ _Рѕер┤х_

----------------------------------------------------------------------
-- Propositional cofibrations
----------------------------------------------------------------------

postulate
  CofProp : Type
  [_] : CofProp Рєњ Type

  _РѕІ_РЅѕ_ : (S : Shape) Рєњ РЪе S РЪЕ Рєњ РЪе S РЪЕ Рєњ CofProp
  [РЅѕ] : (S : Shape) (s t : РЪе S РЪЕ) Рєњ [ S РѕІ s РЅѕ t ] РЅА (s РЅА t)

  РіЦ : CofProp
  [РіЦ] : [ РіЦ ] РЅА ­ЮЪў

  _Рѕе_ : CofProp Рєњ CofProp Рєњ CofProp
  [Рѕе] : Рѕђ ¤є ¤ѕ Рєњ [ ¤є Рѕе ¤ѕ ] РЅА РѕЦ [ ¤є ] Ріј [ ¤ѕ ] РѕЦ

  all : (S : Shape) Рєњ (РЪе S РЪЕ Рєњ CofProp) Рєњ CofProp
  [all] : Рѕђ S ¤є Рєњ [ all S ¤є ] РЅА ((s : РЪе S РЪЕ) Рєњ [ ¤є s ])

  {-# REWRITE [РЅѕ] [РіЦ] [Рѕе] [all] #-}

  cofIsProp : (¤є : CofProp) Рєњ isProp [ ¤є ]

  shapeРєњРѕе : (S : Shape) (¤є ¤ѕ : РЪе S РЪЕ Рєњ CofProp)
    Рєњ [ all S (╬╗ s Рєњ ¤є s Рѕе ¤ѕ s) ] Рєњ [ all S ¤є Рѕе all S ¤ѕ ]

  РЅѕEquivariant : {S T : Shape} (¤Ѓ : ShapeHom S T) Рєњ (r s : РЪе S РЪЕ)
    Рєњ (T РѕІ РЪф ¤Ѓ РЪФ r РЅѕ РЪф ¤Ѓ РЪФ s) РЅА (S РѕІ r РЅѕ s)

  allEquivariant : {S T : Shape} (¤Ѓ : ShapeHom S T) (╬д : РЪе T РЪЕ Рєњ CofProp)
    Рєњ all T ╬д РЅА all S (╬д Рѕў РЪф ¤Ѓ РЪФ)

----------------------------------------------------------------------
-- Shorthands
----------------------------------------------------------------------

Рѕѓ : ­ЮЋђ Рєњ CofProp
Рѕѓ i = ­ЮЋџ РѕІ i РЅѕ 0 Рѕе ­ЮЋџ РѕІ i РЅѕ 1

Рѕеl : {A : Type РёЊ} {B : Type РёЊ'} Рєњ A Рєњ РѕЦ A Ріј B РѕЦ
Рѕеl a = РѕБ inl a РѕБ

Рѕеr : {A : Type РёЊ} {B : Type РёЊ'} Рєњ B Рєњ РѕЦ A Ріј B РѕЦ
Рѕеr b = РѕБ inr b РѕБ

_,[_] : (╬Њ : Type РёЊ) (╬д : ╬Њ Рєњ CofProp) Рєњ Type РёЊ
╬Њ ,[ ╬д ] = ╬Б x Рѕѕ ╬Њ , [ ╬д x ]

wk[_] : {╬Њ : Type РёЊ} (╬д : ╬Њ Рєњ CofProp)
  Рєњ ╬Њ ,[ ╬д ] Рєњ ╬Њ
wk[ ╬д ] = fst

_РѕІ_РЅѕр┤х_ : {╬Њ : Type РёЊ} (S : Shape)
  Рєњ (╬Њ Рєњ РЪе S РЪЕ) Рєњ (╬Њ Рєњ РЪе S РЪЕ) Рєњ (╬Њ Рєњ CofProp)
(S РѕІ r РЅѕр┤х s) ╬│ = S РѕІ r ╬│ РЅѕ s ╬│

_Рѕер┤х_ : {╬Њ : Type РёЊ} Рєњ (¤є ¤ѕ : ╬Њ Рєњ CofProp) Рєњ (╬Њ Рєњ CofProp)
(¤є Рѕер┤х ¤ѕ) ╬│ = ¤є ╬│ Рѕе ¤ѕ ╬│

----------------------------------------------------------------------
-- Restricted types
----------------------------------------------------------------------

record _[_Рєд_] (A : Type РёЊ) (¤є : CofProp) (a : [ ¤є ] Рєњ A) : Type РёЊ where
  constructor makeRestrict
  field
    out : A
    outРЅА : Рѕђ u Рєњ a u РЅА out

open _[_Рєд_] public

restrictExt : {A : Type РёЊ} {¤є : CofProp} {a : [ ¤є ] Рєњ A}
  {z z' : A [ ¤є Рєд a ]}
  Рєњ z .out РЅА z' .out
  Рєњ z РЅА z'
restrictExt refl = cong (makeRestrict _) (funext ╬╗ _ Рєњ uipImp)

----------------------------------------------------------------------
-- Combining compatible partial functions
----------------------------------------------------------------------

Рѕе-rec : {A : Type РёЊ}
  (¤є ¤ѕ : CofProp)
  (f : [ ¤є ] Рєњ A)
  (g : [ ¤ѕ ] Рєњ A)
  .(p : Рѕђ u v Рєњ f u РЅА g v)
  Рєњ ---------------------------
  [ ¤є Рѕе ¤ѕ ] Рєњ A
Рѕе-rec {A = A} ¤є ¤ѕ f g p =
  РѕЦРѕЦ-rec _ [ f РѕБ g ] ╬╗
    { (inl _) (inl _) Рєњ cong f (cofIsProp ¤є _ _)
    ; (inl u) (inr v) Рєњ p u v
    ; (inr v) (inl u) Рєњ sym (p u v)
    ; (inr _) (inr _) Рєњ cong g (cofIsProp ¤ѕ _ _)}

OI-rec : (r : ­ЮЋђ) {A : Type РёЊ}
  Рєњ ([ ­ЮЋџ РѕІ r РЅѕ 0 ] Рєњ A)
  Рєњ ([ ­ЮЋџ РѕІ r РЅѕ 1 ] Рєњ A)
  Рєњ ---------------------------
  [ Рѕѓ r ] Рєњ A
OI-rec r f g =
  Рѕе-rec (­ЮЋџ РѕІ r РЅѕ 0) (­ЮЋџ РѕІ r РЅѕ 1) f g
    (╬╗ u v Рєњ 0РЅа1 (sym u РѕЎ v))

Рѕе-elim : (¤є ¤ѕ : CofProp)
  (P : [ ¤є Рѕе ¤ѕ ] Рєњ Type РёЊ)
  (f : (u : [ ¤є ]) Рєњ P (Рѕеl u))
  (g : (v : [ ¤ѕ ]) Рєњ P (Рѕеr v))
  .(p : (u : [ ¤є ]) (v : [ ¤ѕ ]) Рєњ subst P (trunc _ _) (f u) РЅА g v)
  Рєњ ---------------------------
  (w : [ ¤є Рѕе ¤ѕ ]) Рєњ P w
Рѕе-elim ¤є ¤ѕ P f g p w =
  subst P (trunc _ _) (Рѕе-rec╬Б w .snd)
  where
  Рѕе-rec╬Б : [ ¤є Рѕе ¤ѕ ] Рєњ ╬Б _ P
  Рѕе-rec╬Б =
    Рѕе-rec ¤є ¤ѕ
      (╬╗ u Рєњ Рѕеl u , f u)
      (╬╗ v Рєњ Рѕеr v , g v)
      (╬╗ u v Рєњ ╬Бext (trunc _ _) (p u v))

Рѕе-elimProp : (¤є ¤ѕ : CofProp)
  (P : [ ¤є Рѕе ¤ѕ ] Рєњ Type РёЊ)
  (propP : Рѕђ uv Рєњ isProp (P uv))
  (f : (u : [ ¤є ]) Рєњ P (Рѕеl u))
  (g : (v : [ ¤ѕ ]) Рєњ P (Рѕеr v))
  Рєњ ---------------------------
  (w : [ ¤є Рѕе ¤ѕ ]) Рєњ P w
Рѕе-elimProp ¤є ¤ѕ P propP f g =
  Рѕе-elim ¤є ¤ѕ _ f g (╬╗ _ _ Рєњ propP _ _ _)

OI-elim : (r : ­ЮЋђ)
  {A : [ Рѕѓ r ] Рєњ Type РёЊ}
  Рєњ ((rO : [ ­ЮЋџ РѕІ r РЅѕ 0 ]) Рєњ A (Рѕеl rO))
  Рєњ ((rI : [ ­ЮЋџ РѕІ r РЅѕ 1 ]) Рєњ A (Рѕеr rI))
  Рєњ ---------------------------
  (rOI : [ Рѕѓ r ]) Рєњ A rOI
OI-elim r f g =
  Рѕе-elim (­ЮЋџ РѕІ r РЅѕ 0) (­ЮЋџ РѕІ r РЅѕ 1) _ f g (╬╗ {refl rРЅАI Рєњ 0РЅа1 rРЅАI})

opaque
  Рѕе-elimEq : (¤є ¤ѕ : CofProp) {A : [ ¤є Рѕе ¤ѕ ] Рєњ Type РёЊ}
    {f g : (uv : [ ¤є Рѕе ¤ѕ ]) Рєњ A uv}
    Рєњ ((u : [ ¤є ]) Рєњ f (Рѕеl u) РЅА g (Рѕеl u))
    Рєњ ((v : [ ¤ѕ ]) Рєњ f (Рѕеr v) РЅА g (Рѕеr v))
    Рєњ ---------------------------
    (w : [ ¤є Рѕе ¤ѕ ]) Рєњ f w РЅА g w
  Рѕе-elimEq ¤є ¤ѕ =
    Рѕе-elimProp ¤є ¤ѕ _ (╬╗ _ Рєњ uip)

opaque
  takeOutCof : {A : Type РёЊ} (¤є ¤єРѓђ ¤єРѓЂ : CofProp)
    {fРѓђ : [ ¤є Рѕе ¤єРѓђ ] Рєњ A} {fРѓЂ : [ ¤є Рѕе ¤єРѓЂ ] Рєњ A}
    Рєњ (Рѕђ u Рєњ fРѓђ (Рѕеl u) РЅА fРѓЂ (Рѕеl u))
    Рєњ (Рѕђ vРѓђ vРѓЂ Рєњ fРѓђ (Рѕеr vРѓђ) РЅА fРѓЂ (Рѕеr vРѓЂ))
    Рєњ (Рѕђ uvРѓђ uvРѓЂ Рєњ fРѓђ uvРѓђ РЅА fРѓЂ uvРѓЂ)
  takeOutCof ¤є ¤єРѓђ ¤єРѓЂ {fРѓђ} {fРѓЂ} p q =
    Рѕе-elim ¤є ¤єРѓђ _
      (╬╗ uРѓђ Рєњ Рѕе-elimEq ¤є ¤єРѓЂ
        (╬╗ uРѓЂ Рєњ cong fРѓђ (trunc _ _) РѕЎ p uРѓЂ)
        (╬╗ vРѓЂ Рєњ p uРѓђ РѕЎ (cong fРѓЂ (trunc _ _))))
      (╬╗ vРѓђ Рєњ Рѕе-elimEq ¤є ¤єРѓЂ
        (╬╗ uРѓЂ Рєњ cong fРѓђ (trunc _ _) РѕЎ p uРѓЂ)
        (╬╗ vРѓЂ Рєњ q vРѓђ vРѓЂ))
      (╬╗ _ _ Рєњ funext ╬╗ _ Рєњ uipImp)

diagonalElim : (¤є : CofProp) {P : [ ¤є ] Рєњ [ ¤є ] Рєњ Type РёЊ}
  Рєњ (Рѕђ u Рєњ P u u)
  Рєњ (Рѕђ u v Рєњ P u v)
diagonalElim ¤є {P = P} f u v = subst (P u) (cofIsProp ¤є u v) (f u)
