{-

Definitions of contractibility and equivalences.

-}
{-# OPTIONS --rewriting #-}
module type-formers.equivs where

open import prelude
open import axioms
open import fibration.fibration
open import fibration.coercion
open import type-formers.paths
open import type-formers.pi
open import type-formers.sigma

private variable
  â„“ â„“' : Level
  Î“ Î” : Type â„“

------------------------------------------------------------------------------------------
-- Homotopy-contractibility
------------------------------------------------------------------------------------------

IsContr : Type â„“ â†’ Type â„“
IsContr A = Î£ aâ‚€ âˆˆ A , ((a : A) â†’ a ~ aâ‚€)

IsContrá´µ : (Î“ â†’ Type â„“) â†’ (Î“ â†’ Type â„“)
IsContrá´µ A x = IsContr (A x)

opaque
  IsContrFibStr : {A : Î“ â†’ Type â„“} (Î± : FibStr A) â†’ FibStr (IsContrá´µ A)
  IsContrFibStr Î±  =
    Î£FibStr Î± (Î FibStr (Î± âˆ˜á¶ Ë¢ fst) (PathFibStr (Î± âˆ˜á¶ Ë¢ fst âˆ˜á¶ Ë¢ fst) snd (snd âˆ˜ fst)))

  reindexIsContrFibStr : {A : Î“ â†’ Type â„“} {Î± : FibStr A} (Ï : Î” â†’ Î“)
    â†’ IsContrFibStr Î± âˆ˜á¶ Ë¢ Ï â‰¡ IsContrFibStr (Î± âˆ˜á¶ Ë¢ Ï)
  reindexIsContrFibStr Ï =
    reindexÎ£FibStr _
    âˆ™ cong (Î£FibStr _) (reindexÎ FibStr _ âˆ™ cong (Î FibStr _) (reindexPathFibStr _))

IsContrá¶  : Î“ âŠ¢á¶ Type â„“ â†’ Î“ âŠ¢á¶ Type â„“
IsContrá¶  A .fst = IsContrá´µ (A .fst)
IsContrá¶  A .snd = IsContrFibStr (A .snd)

------------------------------------------------------------------------------------------
-- Equivalences
------------------------------------------------------------------------------------------

IsEquiv : {A : Type â„“} {B : Type â„“'} â†’ (A â†’ B) â†’ Type (â„“ âŠ” â„“')
IsEquiv f = âˆ€ b â†’ IsContr (Fiber f b)

IsEquivá´µ : {A : Î“ â†’ Type â„“} {B : Î“ â†’ Type â„“'} (f : Î“ âŠ¢ A â†’á´µ B)
  â†’ Î“ â†’ Type (â„“ âŠ” â„“')
IsEquivá´µ f = Î á´µ _ (IsContrá´µ (Fiberá´µ (f âˆ˜ fst) snd))

opaque
  IsEquivFibStr : {A : Î“ â†’ Type â„“} (Î± : FibStr A) {B : Î“ â†’ Type â„“'} (Î² : FibStr B)
    (f : Î“ âŠ¢ A â†’á´µ B) â†’ FibStr (IsEquivá´µ f)
  IsEquivFibStr Î± Î² f =
    Î FibStr Î² (IsContrFibStr (FiberFibStr (Î± âˆ˜á¶ Ë¢ fst) (Î² âˆ˜á¶ Ë¢ fst) (f âˆ˜ fst) snd))

  reindexIsEquivFibStr : {A : Î“ â†’ Type â„“} {Î± : FibStr A} {B : Î“ â†’ Type â„“'} {Î² : FibStr B}
    {f : Î“ âŠ¢ A â†’á´µ B}
    (Ï : Î” â†’ Î“)
    â†’ IsEquivFibStr Î± Î² f âˆ˜á¶ Ë¢ Ï â‰¡ IsEquivFibStr (Î± âˆ˜á¶ Ë¢ Ï) (Î² âˆ˜á¶ Ë¢ Ï) (f âˆ˜ Ï)
  reindexIsEquivFibStr Ï =
    reindexÎ FibStr _
    âˆ™ cong (Î FibStr _)
        (reindexIsContrFibStr _
          âˆ™ cong IsContrFibStr (reindexFiberFibStr _))

IsEquivá¶  : (A : Î“ âŠ¢á¶ Type â„“) (B : Î“ âŠ¢á¶ Type â„“') (f : Î“ âŠ¢á¶  A â†’á¶  B)
  â†’ Î“ âŠ¢á¶ Type (â„“ âŠ” â„“')
IsEquivá¶  A B f .fst = IsEquivá´µ f
IsEquivá¶  A B f .snd = IsEquivFibStr (A .snd) (B .snd) f

Equiv : (A : Type â„“) (B : Type â„“') â†’ Type (â„“ âŠ” â„“')
Equiv A B = Î£ (A â†’ B) IsEquiv

Equivá´µ : (A : Î“ â†’ Type â„“) (B : Î“ â†’ Type â„“') â†’ (Î“ â†’ Type (â„“ âŠ” â„“'))
Equivá´µ A B = Î£á´µ (A â†’á´µ B) (IsEquivá´µ snd)

-- TODO rename?
equivFun : {A : Î“ â†’ Type â„“} {B : Î“ â†’ Type â„“'} â†’ Î“ âŠ¢ Equivá´µ A B â†’ Î“ âŠ¢ A â†’á´µ B
equivFun = fst âˆ˜_

opaque
  EquivFibStr : {A : Î“ â†’ Type â„“} (Î± : FibStr A) {B : Î“ â†’ Type â„“'} (Î² : FibStr B)
    â†’ FibStr (Equivá´µ A B)
  EquivFibStr Î± Î² =
    Î£FibStr (Î FibStr Î± (Î² âˆ˜á¶ Ë¢ fst)) (IsEquivFibStr (Î± âˆ˜á¶ Ë¢ fst) (Î² âˆ˜á¶ Ë¢ fst) snd)

  reindexEquivFibStr : {A : Î“ â†’ Type â„“} {Î± : FibStr A} {B : Î“ â†’ Type â„“'} {Î² : FibStr B}
    (Ï : Î” â†’ Î“) â†’ EquivFibStr Î± Î² âˆ˜á¶ Ë¢ Ï â‰¡ EquivFibStr (Î± âˆ˜á¶ Ë¢ Ï) (Î² âˆ˜á¶ Ë¢ Ï)
  reindexEquivFibStr Ï =
    reindexÎ£FibStr _
    âˆ™ congâ‚‚ (Î» Î± Î² â†’ Î£FibStr Î± Î²) (reindexÎ FibStr _) (reindexIsEquivFibStr _)

Equivá¶  : (A : Î“ âŠ¢á¶ Type â„“) (B : Î“ âŠ¢á¶ Type â„“') â†’ Î“ âŠ¢á¶ Type (â„“ âŠ” â„“')
Equivá¶  A B .fst = Equivá´µ (A .fst) (B .fst)
Equivá¶  A B .snd = EquivFibStr (A .snd) (B .snd)

reindexEquivá¶  : {A : Î“ âŠ¢á¶ Type â„“} {B : Î“ âŠ¢á¶ Type â„“'}
  (Ï : Î” â†’ Î“) â†’ Equivá¶  A B âˆ˜á¶  Ï â‰¡ Equivá¶  (A âˆ˜á¶  Ï) (B âˆ˜á¶  Ï)
reindexEquivá¶  Ï = Î£ext refl (reindexEquivFibStr _)

------------------------------------------------------------------------------------------
-- Identity and coercion maps are equivalences
------------------------------------------------------------------------------------------

--- TODO use existing proof of singleton contractibility
idEquiv : {A : Type â„“} â†’ FibStr (Î» (_ : ğŸ™) â†’ A) â†’ Equiv A A
idEquiv Î± .fst a = a
idEquiv Î± .snd a .fst = (a , refl~ a)
idEquiv {A = A} Î± .snd a .snd (a' , p) = h
  where
  qBox : (i : ğ•€) â†’ OpenBox ğ•š 1 (Î» _ â†’ A)
  qBox i .cof = âˆ‚ i
  qBox i .tube j = OI-rec i (Î» {refl â†’ p .at j}) (Î» {refl â†’ a})
  qBox i .cap .out = a
  qBox i .cap .outâ‰¡ = OI-elim i (Î» {refl â†’ p .at1}) (Î» {refl â†’ refl})

  q : (i : ğ•€) â†’ Filler (qBox i)
  q i = Î± .lift ğ•š 1 (Î» _ â†’ _) (qBox i)

  h : (a' , p) ~ (a , refl~ a)
  h .at i .fst = q i .fill 0 .out
  h .at i .snd = path (Î» j â†’ q i .fill j .out) refl (q i .capâ‰¡)
  h .at0 =
    FiberExt
      (sym (q 0 .fill 0 .outâ‰¡ (âˆ¨l refl)) âˆ™ p .at0)
      (Î» j â†’ sym (q 0 .fill j .outâ‰¡ (âˆ¨l refl)))
  h .at1 =
    FiberExt
      (sym (q 1 .fill 0 .outâ‰¡ (âˆ¨r refl)))
      (Î» j â†’ sym (q 1 .fill j .outâ‰¡ (âˆ¨r refl)))

idEquivá¶  : (A : Î“ âŠ¢á¶ Type â„“) â†’ Î“ âŠ¢á¶  Equivá¶  A A
idEquivá¶  (_ , Î±) Î³ = idEquiv (Î± âˆ˜á¶ Ë¢ Î» _ â†’ Î³)

opaque
  coerceEquiv : (S : Shape)
    (A : âŸ¨ S âŸ© âŠ¢á¶ Type â„“ )
    (r s : âŸ¨ S âŸ©) â†’ Equiv (A .fst r) (A .fst s)
  coerceEquiv S A r s =
    Coerce.coerce S r (Equivá¶  (A âˆ˜á¶  (Î» _ â†’ r)) A) (idEquivá¶  A r) s

  coerceEquivCap : (S : Shape)
    (A : âŸ¨ S âŸ© âŠ¢á¶ Type â„“)
    (r : âŸ¨ S âŸ©) â†’ coerceEquiv S A r r â‰¡ idEquivá¶  A r
  coerceEquivCap S A r =
    Coerce.capâ‰¡ S r
      (Equivá¶  (A âˆ˜á¶  (Î» _ â†’ r)) A)
      (idEquivá¶  A r)

  coerceEquivVary : âˆ€ {â„“} {S T : Shape} (Ïƒ : ShapeHom S T)
    (A : âŸ¨ T âŸ© âŠ¢á¶ Type â„“)
    (r s : âŸ¨ S âŸ©)
    â†’ coerceEquiv T A (âŸª Ïƒ âŸ« r) (âŸª Ïƒ âŸ« s) â‰¡ coerceEquiv S (A âˆ˜á¶  âŸª Ïƒ âŸ«) r s
  coerceEquivVary {S = S} Ïƒ A r s =
    coerceVary Ïƒ r
      (Equivá¶  (A âˆ˜á¶  (Î» _ â†’ âŸª Ïƒ âŸ« r)) A)
      (idEquivá¶  A (âŸª Ïƒ âŸ« r))
      s
    âˆ™
    cong
      (Î» Î² â†’ Coerce.coerce S r (_ , Î²) (idEquivá¶  A (âŸª Ïƒ âŸ« r)) s)
      (Î£eqâ‚‚ (reindexEquivá¶  _) refl)
