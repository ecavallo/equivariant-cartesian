{-

Definitions of contractible, fibers, equivalences.

-}
{-# OPTIONS --rewriting #-}
module type-formers.equivs where

open import prelude
open import axioms
open import fibration.fibration
open import fibration.coercion
open import type-formers.paths
open import type-formers.pi
open import type-formers.sigma

private variable â„“ â„“' â„“'' : Level

----------------------------------------------------------------------
-- IsContr
----------------------------------------------------------------------

IsContr : Set â„“ â†’ Set â„“
IsContr A = Î£ aâ‚€ âˆˆ A , ((a : A) â†’ a ~ aâ‚€)

IsContrá´µ : {Î“ : Set â„“} â†’ (Î“ â†’ Set â„“') â†’ (Î“ â†’ Set â„“')
IsContrá´µ A x = IsContr (A x)

opaque
  IsContrIsFib : {Î“ : Set â„“} {A : Î“ â†’ Set â„“'}
    â†’ isFib A â†’ isFib (IsContrá´µ A)
  IsContrIsFib Î± =
    Î£IsFib
      Î±
      (Î IsFib
        (reindex Î± fst)
        (reindex (PathIsFib Î±) (Î» ((x , aâ‚€) , a) â†’ x , a , aâ‚€)))

  reindexIsContr : {Î” : Set â„“} {Î“ : Set â„“'}
    {A : Î“ â†’ Set â„“''}
    (Î± : isFib A)
    (Ï : Î” â†’ Î“)
    â†’ reindex (IsContrIsFib Î±) Ï â‰¡ IsContrIsFib (reindex Î± Ï)
  reindexIsContr Î± Ï =
    reindexÎ£ _ _ Ï
    âˆ™
    cong (Î£IsFib (reindex Î± Ï))
      (reindexÎ  _ _ (Ï Ã—id)
        âˆ™ cong
            (Î» Î² â†’
              Î IsFib (reindex Î± (Ï âˆ˜ fst))
                (reindex Î² (Î» ((x , aâ‚€) , a) â†’ x , a , aâ‚€)))
            (reindexPath _ Ï))

----------------------------------------------------------------------
-- Fiber type
----------------------------------------------------------------------

Fiber : {A : Set â„“} {B : Set â„“} (f : A â†’ B) (b : B) â†’ Set â„“
Fiber f b = Î£ a âˆˆ _ , f a ~ b

Fiberá´µ : {Î“ : Set â„“} (A B : Î“ â†’ Set â„“')
  â†’ Î£ (Î£ x âˆˆ Î“ , (A x â†’ B x)) (B âˆ˜ fst) â†’ Set â„“'
Fiberá´µ A B = Î£á´µ (A âˆ˜ fst âˆ˜ fst) (Î» (((x , f) , b) , a) â†’ Pathá´µ B (x , f a , b))

opaque
  FiberIsFib : {Î“ : Set â„“} {A B : Î“ â†’ Set â„“'}
    â†’ isFib A â†’ isFib B â†’ isFib (Fiberá´µ A B)
  FiberIsFib {A = A} {B} Î± Î² =
    Î£IsFib
      (reindex Î± (fst âˆ˜ fst))
      (reindex (PathIsFib Î²) (Î» (((x , f) , b) , a) â†’ (x , f a , b)))

  reindexFiber : {Î” : Set â„“} {Î“ : Set â„“'}
    {A B : Î“ â†’ Set â„“''}
    (Î± : isFib A) (Î² : isFib B)
    (Ï : Î” â†’ Î“)
    â†’ reindex (FiberIsFib Î± Î²) (Ï Ã—id Ã—id) â‰¡ FiberIsFib (reindex Î± Ï) (reindex Î² Ï)
  reindexFiber {A = A} {B} Î± Î² Ï =
    reindexÎ£ _ _ (Ï Ã—id Ã—id)
    âˆ™ cong
        (Î» Î´ â†’
          Î£IsFib (reindex Î± (Ï âˆ˜ fst âˆ˜ fst))
            (reindex Î´ (Î» (((x , f) , b) , a) â†’ (x , f a , b))))
        (reindexPath Î² Ï)

FiberExt : {A B : Set â„“} {f : A â†’ B} {b : B} {x y : Fiber f b}
  â†’ x .fst â‰¡ y .fst â†’ (âˆ€ i â†’ x .snd .at i â‰¡ y .snd .at i) â†’ x â‰¡ y
FiberExt refl p = Î£ext refl (PathExt p)

FiberExtDep : {A B : Set â„“} {f : A â†’ B} {b b' : B} (p : b â‰¡ b')
  {x : Fiber f b} {y : Fiber f b'}
  â†’ x .fst â‰¡ y .fst â†’ (âˆ€ i â†’ x .snd .at i â‰¡ y .snd .at i) â†’ subst (Fiber f) p x â‰¡ y
FiberExtDep refl = FiberExt

eqToFiber : {A B : Set â„“} {f : A â†’ B} {b : B} (a : A) â†’ f a â‰¡ b â†’ Fiber f b
eqToFiber a eq = (a , eqToPath eq)

fiberPathEq : {A B : Set â„“} {f : A â†’ B} {b : B} {x y : Fiber f b}
  â†’ x â‰¡ y â†’ âˆ€ k â†’ x .snd .at k â‰¡ y .snd .at k
fiberPathEq refl _ = refl

fiberPathEqDep : {A B : Set â„“} {f : A â†’ B} {b b' : B} (p : b â‰¡ b')
  {x : Fiber f b} {y : Fiber f b'}
  â†’ subst (Fiber f) p x â‰¡ y â†’ âˆ€ k â†’ x .snd .at k â‰¡ y .snd .at k
fiberPathEqDep refl refl _ = refl

fiberDomEqDep : {A B : Set â„“} {f : A â†’ B} {b b' : B} (p : b â‰¡ b')
  {x : Fiber f b} {y : Fiber f b'}
  â†’ subst (Fiber f) p x â‰¡ y â†’ x .fst â‰¡ y .fst
fiberDomEqDep refl refl = refl

----------------------------------------------------------------------
-- Equivalences
----------------------------------------------------------------------

IsEquiv : {A B : Set â„“} â†’ (A â†’ B) â†’ Set â„“
IsEquiv f = âˆ€ b â†’ IsContr (Fiber f b)

IsEquivá´µ : {Î“ : Set â„“} (A B : Î“ â†’ Set â„“')
  â†’ Î£ Î“ (Î» x â†’ A x â†’ B x) â†’ Set â„“'
IsEquivá´µ A B = Î á´µ (B âˆ˜ fst) (IsContrá´µ (Fiberá´µ A B))

IsEquivIsFib : {Î“ : Set â„“} {A B : Î“ â†’ Set â„“'}
  â†’ isFib A â†’ isFib B â†’ isFib (IsEquivá´µ A B)
IsEquivIsFib {A = A} {B} Î± Î² =
  Î IsFib (reindex Î² fst) (IsContrIsFib (FiberIsFib Î± Î²))

reindexIsEquiv : {Î” : Set â„“} {Î“ : Set â„“'} {A B : Î“ â†’ Set â„“''}
  (Î± : isFib A) (Î² : isFib B)
  (Ï : Î” â†’ Î“)
  â†’ reindex (IsEquivIsFib Î± Î²) (Ï Ã—id) â‰¡ IsEquivIsFib (reindex Î± Ï) (reindex Î² Ï)
reindexIsEquiv {A = A} {B} Î± Î² Ï =
  reindexÎ  _ _ (Ï Ã—id)
  âˆ™ cong (Î IsFib (reindex Î² (Ï âˆ˜ fst)))
      (reindexIsContr (FiberIsFib Î± Î²) (Ï Ã—id Ã—id)
        âˆ™ cong IsContrIsFib (reindexFiber Î± Î² Ï))

Equiv : (A B : Set â„“) â†’ Set â„“
Equiv A B = Î£ (A â†’ B) IsEquiv

Equivá´µ : {Î“ : Set â„“} (A B : Î“ â†’ Set â„“') â†’ (Î“ â†’ Set â„“')
Equivá´µ A B = Î£á´µ (Î á´µ A (B âˆ˜ fst)) (IsEquivá´µ A B)

equivFun : {Î“ : Set â„“} {A B : Î“ â†’ Set â„“'}
  â†’ Î  (Equivá´µ A B) â†’ Î  (Î á´µ A (B âˆ˜ fst))
equivFun fe x = fe x .fst

opaque
  EquivIsFib : {Î“ : Set â„“} {A B : Î“ â†’ Set â„“'}
    â†’ isFib A â†’ isFib B â†’ isFib (Equivá´µ A B)
  EquivIsFib {A = A} {B} Î± Î² =
    Î£IsFib (Î IsFib Î± (reindex Î² fst)) (IsEquivIsFib Î± Î²)

  reindexEquiv : {Î” : Set â„“} {Î“ : Set â„“'} {A B : Î“ â†’ Set â„“''}
    (Î± : isFib A) (Î² : isFib B)
    (Ï : Î” â†’ Î“)
    â†’ reindex (EquivIsFib Î± Î²) Ï â‰¡ EquivIsFib (reindex Î± Ï) (reindex Î² Ï)
  reindexEquiv Î± Î² Ï =
    reindexÎ£ _ _ Ï âˆ™ congâ‚‚ Î£IsFib (reindexÎ  _ _ Ï) (reindexIsEquiv Î± Î² Ï)

----------------------------------------------------------------------
-- Identity and coercion maps are equivalences
----------------------------------------------------------------------

idEquiv : {A : Set â„“} â†’ isFib (Î» (_ : ğŸ™) â†’ A) â†’ Equiv A A
idEquiv Î± .fst a = a
idEquiv Î± .snd a .fst = (a , refl~ a)
idEquiv {A = A} Î± .snd a .snd (a' , p) = h
  where
  qBox : (i : ğ•€) â†’ OpenBox ğ•š 1 (Î» _ â†’ A)
  qBox i .cof = âˆ‚ i
  qBox i .tube =
    OI-rec i
      (Î» {refl â†’ p .at})
      (Î» {refl _ â†’ a})
  qBox i .cap .out = a
  qBox i .cap .outâ‰¡ =
    OI-elim i
      (Î» {refl â†’ p .at1})
      (Î» {refl â†’ refl})

  q : (i : ğ•€) â†’ Filler (qBox i)
  q i = Î± .lift ğ•š 1 (Î» _ â†’ _) (qBox i)

  h : (a' , p) ~ (a , refl~ a)
  h .at i .fst = q i .fill 0 .out
  h .at i .snd = path (Î» j â†’ q i .fill j .out) refl (q i .capâ‰¡)
  h .at0 =
    FiberExt
      (sym (q 0 .fill 0 .outâ‰¡ (âˆ¨l refl)) âˆ™ p .at0)
      (Î» j â†’ sym (q 0 .fill j .outâ‰¡ (âˆ¨l refl)))
  h .at1 =
    FiberExt
      (sym (q 1 .fill 0 .outâ‰¡ (âˆ¨r refl)))
      (Î» j â†’ sym (q 1 .fill j .outâ‰¡ (âˆ¨r refl)))

coerceEquiv : (S : Shape) {A : âŸ¨ S âŸ© â†’ Set â„“}
  (Î± : isFib A) (r s : âŸ¨ S âŸ©)
  â†’ Equiv (A r) (A s)
coerceEquiv S {A} Î± r s =
  coerce S r
    (EquivIsFib (reindex Î± (Î» _ â†’ r)) Î±)
    (idEquiv (reindex Î± (Î» _ â†’ r)))
    s

opaque
  coerceEquivCap : (S : Shape) {A : âŸ¨ S âŸ© â†’ Set â„“}
    (Î± : isFib A) (r : âŸ¨ S âŸ©)
    â†’ coerceEquiv S Î± r r â‰¡ idEquiv (reindex Î± (Î» _ â†’ r))
  coerceEquivCap S {A} Î± r =
    coerceCap S r
      (EquivIsFib (reindex Î± (Î» _ â†’ r)) Î±)
      (idEquiv (reindex Î± (Î» _ â†’ r)))

  coerceEquivVary : âˆ€ {â„“} (S T : Shape) (Ïƒ : ShapeHom S T)
    {A : âŸ¨ T âŸ© â†’ Set â„“} (Î± : isFib A) (r s : âŸ¨ S âŸ©)
    â†’ coerceEquiv T Î± (âŸª Ïƒ âŸ« r) (âŸª Ïƒ âŸ« s) â‰¡ coerceEquiv S (reindex Î± âŸª Ïƒ âŸ«) r s
  coerceEquivVary S T Ïƒ {A = A} Î± r s =
    coerceVary S T Ïƒ r
      (EquivIsFib (reindex Î± (Î» _ â†’ âŸª Ïƒ âŸ« r)) Î±)
      (idEquiv (reindex Î± (Î» _ â†’ âŸª Ïƒ âŸ« r)))
      s
    âˆ™
    cong
      (Î» Î² â†’ coerce S r Î² (idEquiv (reindex Î± (Î» _ â†’ âŸª Ïƒ âŸ« r))) s)
      (reindexEquiv (reindex Î± (Î» _ â†’ âŸª Ïƒ âŸ« r)) Î± âŸª Ïƒ âŸ«)
