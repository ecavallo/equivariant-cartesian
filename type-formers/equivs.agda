{-

Definitions of contractible, fibers, equivalences.

-}
{-# OPTIONS --rewriting #-}
module type-formers.equivs where

open import prelude
open import axioms
open import fibration.fibration
open import fibration.coercion
open import type-formers.paths
open import type-formers.pi
open import type-formers.sigma

private variable â„“ â„“' â„“'' â„“''' : Level

------------------------------------------------------------------------------------------
-- Homotopy-contractibility
------------------------------------------------------------------------------------------

IsContr : Type â„“ â†’ Type â„“
IsContr A = Î£ aâ‚€ âˆˆ A , ((a : A) â†’ a ~ aâ‚€)

IsContrá´µ : {Î“ : Type â„“} â†’ (Î“ â†’ Type â„“') â†’ (Î“ â†’ Type â„“')
IsContrá´µ A x = IsContr (A x)

opaque
  IsContrFibStr : {Î“ : Type â„“} {A : Î“ â†’ Type â„“'}
    â†’ FibStr A â†’ FibStr (IsContrá´µ A)
  IsContrFibStr Î± =
    Î£FibStr
      Î±
      (Î FibStr
        (Î± âˆ˜á¶ Ë¢ fst)
        (PathFibStr (Î± âˆ˜á¶ Ë¢ (fst âˆ˜ fst)) snd (snd âˆ˜ fst)))

  reindexIsContrFibStr : {Î” : Type â„“} {Î“ : Type â„“'}
    {A : Î“ â†’ Type â„“''}
    (Î± : FibStr A)
    (Ï : Î” â†’ Î“)
    â†’ IsContrFibStr Î± âˆ˜á¶ Ë¢ Ï â‰¡ IsContrFibStr (Î± âˆ˜á¶ Ë¢ Ï)
  reindexIsContrFibStr Î± Ï =
    reindexÎ£FibStr _ _ _
    âˆ™ cong (Î£FibStr _) (reindexÎ FibStr _ _ _ âˆ™ cong (Î FibStr _) (reindexPathFibStr _ _))

------------------------------------------------------------------------------------------
-- Homotopy fiber type
------------------------------------------------------------------------------------------

Fiber : {A : Type â„“} {B : Type â„“'} (f : A â†’ B) (b : B) â†’ Type (â„“ âŠ” â„“')
Fiber f b = Î£ a âˆˆ _ , f a ~ b

Fiberá´µ : {Î“ : Type â„“} {A : Î“ â†’ Type â„“'} {B : Î“ â†’ Type â„“''}
  (f : Î“ âŠ¢ A â†’á´µ B)
  (b : Î“ âŠ¢ B)
  â†’ Î“ â†’ Type (â„“' âŠ” â„“'')
Fiberá´µ f b Î³ = Fiber (f Î³) (b Î³)

opaque
  FiberFibStr : {Î“ : Type â„“}
    {A : Î“ â†’ Type â„“'} (Î± : FibStr A)
    {B : Î“ â†’ Type â„“''} (b : FibStr B)
    (f : Î“ âŠ¢ A â†’á´µ B)
    (b : Î“ âŠ¢ B)
    â†’ FibStr (Fiberá´µ f b)
  FiberFibStr Î± Î² f b =
    Î£FibStr Î± (PathFibStr (Î² âˆ˜á¶ Ë¢ fst) _ _)

  reindexFiberFibStr : {Î” : Type â„“} {Î“ : Type â„“'}
    {A : Î“ â†’ Type â„“''} (Î± : FibStr A)
    {B : Î“ â†’ Type â„“'''} (Î² : FibStr B)
    {f : Î“ âŠ¢ A â†’á´µ B}
    {b : Î“ âŠ¢ B}
    (Ï : Î” â†’ Î“)
    â†’ FiberFibStr Î± Î² f b âˆ˜á¶ Ë¢ Ï â‰¡ FiberFibStr (Î± âˆ˜á¶ Ë¢ Ï) (Î² âˆ˜á¶ Ë¢ Ï) (f âˆ˜ Ï) (b âˆ˜ Ï)
  reindexFiberFibStr Î± Î² Ï =
    reindexÎ£FibStr _ _ _ âˆ™ cong (Î£FibStr _) (reindexPathFibStr _ _)

module _ {A : Type â„“} {B : Type â„“'} {f : A â†’ B} where

  FiberExt : {b : B} {x y : Fiber f b}
    â†’ x .fst â‰¡ y .fst â†’ (âˆ€ i â†’ x .snd .at i â‰¡ y .snd .at i) â†’ x â‰¡ y
  FiberExt refl p = Î£ext refl (PathExt p)

  FiberExtDep : {b b' : B} (p : b â‰¡ b') {x : Fiber f b} {y : Fiber f b'}
    â†’ x .fst â‰¡ y .fst
    â†’ (âˆ€ i â†’ x .snd .at i â‰¡ y .snd .at i)
    â†’ subst (Fiber f) p x â‰¡ y
  FiberExtDep refl = FiberExt

  eqToFiber : {b : B} (a : A) â†’ f a â‰¡ b â†’ Fiber f b
  eqToFiber a eq = (a , eqToPath eq)

  fiberPathEq : {b : B} {x y : Fiber f b}
    â†’ x â‰¡ y â†’ âˆ€ k â†’ x .snd .at k â‰¡ y .snd .at k
  fiberPathEq refl _ = refl

  fiberPathEqDep : {b b' : B} (p : b â‰¡ b')
    {x : Fiber f b} {y : Fiber f b'}
    â†’ subst (Fiber f) p x â‰¡ y â†’ âˆ€ k â†’ x .snd .at k â‰¡ y .snd .at k
  fiberPathEqDep refl refl _ = refl

  fiberDomEqDep : {b b' : B} (p : b â‰¡ b') {x : Fiber f b} {y : Fiber f b'}
    â†’ subst (Fiber f) p x â‰¡ y â†’ x .fst â‰¡ y .fst
  fiberDomEqDep refl refl = refl

------------------------------------------------------------------------------------------
-- Equivalences
------------------------------------------------------------------------------------------

IsEquiv : {A : Type â„“} {B : Type â„“'} â†’ (A â†’ B) â†’ Type (â„“ âŠ” â„“')
IsEquiv f = âˆ€ b â†’ IsContr (Fiber f b)

IsEquivá´µ : {Î“ : Type â„“} {A : Î“ â†’ Type â„“'} {B : Î“ â†’ Type â„“''}
  (f : Î“ âŠ¢ A â†’á´µ B)
  â†’ Î“ â†’ Type (â„“' âŠ” â„“'')
IsEquivá´µ f = Î á´µ _ (IsContrá´µ (Fiberá´µ (f âˆ˜ fst) snd))

IsEquivFibStr : {Î“ : Type â„“}
  {A : Î“ â†’ Type â„“'} (Î± : FibStr A)
  {B : Î“ â†’ Type â„“''} (Î² : FibStr B)
  (f : Î“ âŠ¢ A â†’á´µ B)
  â†’ FibStr (IsEquivá´µ f)
IsEquivFibStr Î± Î² f =
  Î FibStr Î² (IsContrFibStr (FiberFibStr (Î± âˆ˜á¶ Ë¢ _) (Î² âˆ˜á¶ Ë¢ _) _ _))

reindexIsEquivFibStr : {Î” : Type â„“} {Î“ : Type â„“'}
  {A : Î“ â†’ Type â„“''} (Î± : FibStr A)
  {B : Î“ â†’ Type â„“'''} (Î² : FibStr B)
  {f : Î“ âŠ¢ A â†’á´µ B}
  (Ï : Î” â†’ Î“)
  â†’ IsEquivFibStr Î± Î² f âˆ˜á¶ Ë¢ Ï â‰¡ IsEquivFibStr (Î± âˆ˜á¶ Ë¢ Ï) (Î² âˆ˜á¶ Ë¢ Ï) (f âˆ˜ Ï)
reindexIsEquivFibStr Î± Î² Ï =
  reindexÎ FibStr _ _ _
  âˆ™ cong (Î FibStr _)
      (reindexIsContrFibStr _ _ âˆ™ cong IsContrFibStr (reindexFiberFibStr _ _ _))

Equiv : (A : Type â„“) (B : Type â„“') â†’ Type (â„“ âŠ” â„“')
Equiv A B = Î£ (A â†’ B) IsEquiv

Equivá´µ : {Î“ : Type â„“} (A : Î“ â†’ Type â„“') (B : Î“ â†’ Type â„“'') â†’ (Î“ â†’ Type (â„“' âŠ” â„“''))
Equivá´µ A B = Î£á´µ (A â†’á´µ B) (IsEquivá´µ snd)

-- TODO rename?
equivFun : {Î“ : Type â„“} {A : Î“ â†’ Type â„“'} {B : Î“ â†’ Type â„“''}
  â†’ Î“ âŠ¢ Equivá´µ A B
  â†’ Î“ âŠ¢ A â†’á´µ B
equivFun = fst âˆ˜_

opaque
  EquivFibStr : {Î“ : Type â„“}
    {A : Î“ â†’ Type â„“'} (Î± : FibStr A)
    {B : Î“ â†’ Type â„“''} (Î² : FibStr B)
    â†’ FibStr (Equivá´µ A B)
  EquivFibStr Î± Î² =
    Î£FibStr (Î FibStr Î± (Î² âˆ˜á¶ Ë¢ fst)) (IsEquivFibStr (Î± âˆ˜á¶ Ë¢ fst) (Î² âˆ˜á¶ Ë¢ fst) _)

  reindexEquivFibStr : {Î” : Type â„“} {Î“ : Type â„“'}
    {A : Î“ â†’ Type â„“''} (Î± : FibStr A)
    {B : Î“ â†’ Type â„“'''} (Î² : FibStr B)
    (Ï : Î” â†’ Î“)
    â†’ EquivFibStr Î± Î² âˆ˜á¶ Ë¢ Ï â‰¡ EquivFibStr (Î± âˆ˜á¶ Ë¢ Ï) (Î² âˆ˜á¶ Ë¢ Ï)
  reindexEquivFibStr Î± Î² Ï =
    reindexÎ£FibStr _ _ _
    âˆ™ congâ‚‚ (Î» Î± Î² â†’ Î£FibStr Î± Î²)
        (reindexÎ FibStr _ _ _)
        (reindexIsEquivFibStr (Î± âˆ˜á¶ Ë¢ fst) _ _)

------------------------------------------------------------------------------------------
-- Identity and coercion maps are equivalences
------------------------------------------------------------------------------------------

idEquiv : {A : Type â„“} â†’ FibStr (Î» (_ : ğŸ™) â†’ A) â†’ Equiv A A
idEquiv Î± .fst a = a
idEquiv Î± .snd a .fst = (a , refl~ a)
idEquiv {A = A} Î± .snd a .snd (a' , p) = h
  where
  qBox : (i : ğ•€) â†’ OpenBox ğ•š 1 (Î» _ â†’ A)
  qBox i .cof = âˆ‚ i
  qBox i .tube = OI-rec i (Î» {refl â†’ p .at}) (Î» {refl _ â†’ a})
  qBox i .cap .out = a
  qBox i .cap .outâ‰¡ = OI-elim i (Î» {refl â†’ p .at1}) (Î» {refl â†’ refl})

  q : (i : ğ•€) â†’ Filler (qBox i)
  q i = Î± .lift ğ•š 1 (Î» _ â†’ _) (qBox i)

  h : (a' , p) ~ (a , refl~ a)
  h .at i .fst = q i .fill 0 .out
  h .at i .snd = path (Î» j â†’ q i .fill j .out) refl (q i .capâ‰¡)
  h .at0 =
    FiberExt
      (sym (q 0 .fill 0 .outâ‰¡ (âˆ¨l refl)) âˆ™ p .at0)
      (Î» j â†’ sym (q 0 .fill j .outâ‰¡ (âˆ¨l refl)))
  h .at1 =
    FiberExt
      (sym (q 1 .fill 0 .outâ‰¡ (âˆ¨r refl)))
      (Î» j â†’ sym (q 1 .fill j .outâ‰¡ (âˆ¨r refl)))

idEquivFib : (A : Fib â„“ ğŸ™) â†’ Equiv (A .fst tt) (A .fst tt)
idEquivFib (_ , Î±) = idEquiv Î±

coerceEquiv : (S : Shape)
  {A : âŸ¨ S âŸ© â†’ Type â„“} (Î± : FibStr A)
  (r s : âŸ¨ S âŸ©) â†’ Equiv (A r) (A s)
coerceEquiv S {A} Î± r s =
  coerce S r
    (EquivFibStr (Î± âˆ˜á¶ Ë¢ (Î» _ â†’ r)) Î±)
    (idEquiv (Î± âˆ˜á¶ Ë¢ (Î» _ â†’ r)))
    s

opaque
  coerceEquivCap : (S : Shape)
    {A : âŸ¨ S âŸ© â†’ Type â„“} (Î± : FibStr A)
    (r : âŸ¨ S âŸ©) â†’ coerceEquiv S Î± r r â‰¡ idEquiv (Î± âˆ˜á¶ Ë¢ (Î» _ â†’ r))
  coerceEquivCap S {A} Î± r =
    coerceCap S r
      (EquivFibStr (Î± âˆ˜á¶ Ë¢ (Î» _ â†’ r)) Î±)
      (idEquiv (Î± âˆ˜á¶ Ë¢ (Î» _ â†’ r)))

  coerceEquivVary : âˆ€ {â„“} {S T : Shape} (Ïƒ : ShapeHom S T)
    {A : âŸ¨ T âŸ© â†’ Type â„“} (Î± : FibStr A)
    (r s : âŸ¨ S âŸ©)
    â†’ coerceEquiv T Î± (âŸª Ïƒ âŸ« r) (âŸª Ïƒ âŸ« s) â‰¡ coerceEquiv S (Î± âˆ˜á¶ Ë¢ âŸª Ïƒ âŸ«) r s
  coerceEquivVary {S = S} Ïƒ Î± r s =
    coerceVary Ïƒ r
      (EquivFibStr (Î± âˆ˜á¶ Ë¢ (Î» _ â†’ âŸª Ïƒ âŸ« r)) Î±)
      (idEquiv (Î± âˆ˜á¶ Ë¢ (Î» _ â†’ âŸª Ïƒ âŸ« r)))
      s
    âˆ™
    cong
      (Î» Î² â†’ coerce S r Î² (idEquiv (Î± âˆ˜á¶ Ë¢ (Î» _ â†’ âŸª Ïƒ âŸ« r))) s)
      (reindexEquivFibStr (Î± âˆ˜á¶ Ë¢ (Î» _ â†’ âŸª Ïƒ âŸ« r)) Î± âŸª Ïƒ âŸ«)
