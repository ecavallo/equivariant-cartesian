{-

Fibration structure on Swan identity types, assuming a dominance and cofibration extensionality.

-}
module type-formers.swan-identity where

open import prelude
open import axioms
open import fibration.fibration
open import fibration.trivial
open import type-formers.paths
open import type-formers.sigma

private variable
  â„“ â„“' : Level
  Î“ Î” : Type â„“

module SwanIdentity
  (_âˆ§_ : (Ï† : Cof) â†’ ([ Ï† ] â†’ Cof) â†’ Cof)
  ([âˆ§] : âˆ€ Ï† Ïˆ â†’ [ Ï† âˆ§ Ïˆ ] â‰¡ Î£ [ Ï† ] ([_] âˆ˜ Ïˆ))
  (cofExt : {Ï† Ïˆ : Cof} â†’ ([ Ï† ] â†’ [ Ïˆ ]) â†’ ([ Ïˆ ] â†’ [ Ï† ]) â†’ Ï† â‰¡ Ïˆ)
  where

  private
    âˆ§-pair : âˆ€ {Ï† Ïˆ} â†’ (u : [ Ï† ]) â†’ [ Ïˆ u ] â†’ [ Ï† âˆ§ Ïˆ ]
    âˆ§-pair u v = coe (sym ([âˆ§] _ _)) (u , v)

    âˆ§-fst : âˆ€ {Ï† Ïˆ} â†’ [ Ï† âˆ§ Ïˆ ] â†’ [ Ï† ]
    âˆ§-fst uv = coe ([âˆ§] _ _) uv .fst

    âˆ§-snd : âˆ€ {Ï† Ïˆ} â†’ (uv : [ Ï† âˆ§ Ïˆ ]) â†’ [ Ïˆ (âˆ§-fst uv) ]
    âˆ§-snd uv = coe ([âˆ§] _ _) uv .snd

  Constancy : {A : Type â„“} {aâ‚€ aâ‚ : A} (p : aâ‚€ ~ aâ‚) â†’ Type â„“
  Constancy p = Î£ Ï† âˆˆ Cof , ((i : ğ•€) â†’ [ Ï† ] â†’ p .at i â‰¡ p .at 0)

  ConstancyExt : {A : Type â„“} {aâ‚€ aâ‚ : A} (p : aâ‚€ ~ aâ‚) {câ‚€ câ‚ : Constancy p}
    â†’ câ‚€ .fst â‰¡ câ‚ .fst
    â†’ câ‚€ â‰¡ câ‚
  ConstancyExt _ eq = Î£ext eq (funExt' $ funExt' uip')

  Id : {A : Type â„“} (aâ‚€ aâ‚ : A) â†’ Type â„“
  Id aâ‚€ aâ‚ = Î£ (aâ‚€ ~ aâ‚) Constancy

  IdExt : {A : Type â„“} {aâ‚€ aâ‚ : A} {qâ‚€ qâ‚ : Id aâ‚€ aâ‚}
    â†’ (âˆ€ i â†’ qâ‚€ .fst .at i â‰¡ qâ‚ .fst .at i)
    â†’ qâ‚€ .snd .fst â‰¡ qâ‚ .snd .fst
    â†’ qâ‚€ â‰¡ qâ‚
  IdExt {qâ‚€ = qâ‚€} {qâ‚} eqâ‚€ eqâ‚ = lemma (PathExt eqâ‚€)
    where
    lemma : qâ‚€ .fst â‰¡ qâ‚ .fst â†’ qâ‚€ â‰¡ qâ‚
    lemma refl = Î£ext refl (ConstancyExt (qâ‚€ .fst) eqâ‚)

  Constancyá´µ : {A : Î“ â†’ Type â„“} {aâ‚€ aâ‚ : Î“ âŠ¢ A} (p : Î“ âŠ¢ Pathá´µ A aâ‚€ aâ‚) â†’ (Î“ â†’ Type â„“)
  Constancyá´µ p Î³ = Constancy (p Î³)

  Idá´µ : (A : Î“ â†’ Type â„“) (aâ‚€ aâ‚ : Î“ âŠ¢ A) â†’ (Î“ â†’ Type â„“)
  Idá´µ A aâ‚€ aâ‚ Î³ = Id (aâ‚€ Î³) (aâ‚ Î³)

  ConstancyIsTFib : {A : Î“ â†’ Type â„“} {aâ‚€ aâ‚ : Î“ âŠ¢ A} (p : Î“ âŠ¢ Pathá´µ A aâ‚€ aâ‚)
    â†’ TFibStr (Constancyá´µ p)
  ConstancyIsTFib p Î³ Ï† a .out .fst = Ï† âˆ§ Î» u â†’ a u .fst
  ConstancyIsTFib p Î³ Ï† a .out .snd i uv = a (âˆ§-fst uv) .snd i (âˆ§-snd uv)
  ConstancyIsTFib p Î³ Ï† a .outâ‰¡ u =
    ConstancyExt (p Î³)
      (cofExt (âˆ§-pair u) (subst (Î» u â†’ [ a u .fst ]) (cofIsProp' Ï†) âˆ˜ âˆ§-snd))

  Idá¶  : (A : Î“ âŠ¢á¶ Type â„“) (aâ‚€ aâ‚ : Î“ âŠ¢á¶  A) â†’ Î“ âŠ¢á¶ Type â„“
  Idá¶  A aâ‚€ aâ‚ = Î£á¶  (Pathá¶  A aâ‚€ aâ‚) (TFibToFib (_ , ConstancyIsTFib snd))

  idreflá¶  : (A : Î“ âŠ¢á¶ Type â„“) (a : Î“ âŠ¢á¶  A) â†’ Î“ âŠ¢á¶  Idá¶  A a a
  idreflá¶  A a Î³ .fst = refl~ (a Î³)
  idreflá¶  A a Î³ .snd .fst = ğ•š âˆ‹ 0 â‰ˆ 0
  idreflá¶  A a Î³ .snd .snd _ _ = refl

  -- TODO J
