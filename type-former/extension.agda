{-

Fibrancy of extension types over shapes.

-}
module type-former.extension where

open import basic
open import internal-extensional-type-theory
open import axiom
open import cofibration
open import fibration.fibration

private variable
  â„“ : Level
  Î“ Î” : Type â„“

------------------------------------------------------------------------------------------
-- Definition of an extension type over a shape Z, type A over âŸ¨ Z âŸ©, and partial element
-- of A in the context extended by Z. Elements are functions from Z to A which extend the
-- partial element.
--
-- For this to define a fibrant type when A is fibrant, it is important that the domain of
-- definition of the partial element (Ï† below) depend only on Z and not on the ambient
-- context.
------------------------------------------------------------------------------------------

ExtensionË£ : (Z : Shape)
  (A : Î“ â–·âŸ¨ Z âŸ© â†’ Type â„“)
  (Ï† : âŸ¨ Z âŸ© â†’ Cof)
  (a : Î“ â–·âŸ¨ Z âŸ© â–·[ Ï† âˆ˜ ğ’’ ] âŠ¢Ë£ A â†¾ (Ï† âˆ˜ ğ’’))
  â†’ Î“ â†’ Type â„“
ExtensionË£ Z A Ï† a Î³ =
  (z : âŸ¨ Z âŸ©) â†’ A (Î³ , z) [ Ï† z â†¦ curry a (Î³ , z) ]

------------------------------------------------------------------------------------------
-- Fibrancy of extension types.
------------------------------------------------------------------------------------------

module ExtensionLift {Z Ï† S r}
  {A : âŸ¨ S âŸ© â–·âŸ¨ Z âŸ© â†’ Type â„“} (Î± : FibStr A)
  {a : âŸ¨ S âŸ© â–·âŸ¨ Z âŸ© â–·[ Ï† âˆ˜ ğ’’ ] âŠ¢Ë£ A â†¾ (Ï† âˆ˜ ğ’’)}
  (box : OpenBox S (ExtensionË£ Z A Ï† a) r)
  where

  module _ (z : âŸ¨ Z âŸ©) where

    pointwiseBox : OpenBox S (A âˆ˜ (_, z)) r
    pointwiseBox =
      addToTube
        (mapBox (out âˆ˜ (_$ z)) box)
        (Ï† z)
        (Î» i v â†’ Î» where
          .out â†’ a (i , z , v)
          .outâ‰¡ u â†’ sym (box .tube i u z .outâ‰¡ v))
        (Î» v â†’ box .cap .out z .outâ‰¡ v)

    pointwiseFill = Î± .lift S (_, z) r pointwiseBox

  filler : Filler box
  filler .fill s .out z .out = pointwiseFill z .fill s .out
  filler .fill s .out z .outâ‰¡ v = pointwiseFill z .fill s .outâ‰¡ (âˆ¨r v)
  filler .fill s .outâ‰¡ u =
    funExt Î» z â†’ restrictExt (pointwiseFill z .fill s .outâ‰¡ (âˆ¨l u))
  filler .capâ‰¡ = funExt Î» z â†’ restrictExt (pointwiseFill z .capâ‰¡)

module ExtensionVary {Z Ï† S T} (Ïƒ : Shape[ S , T ]) {r}
  {A : âŸ¨ T âŸ© â–·âŸ¨ Z âŸ© â†’ Type â„“} (Î± : FibStr A)
  {a : âŸ¨ T âŸ© â–·âŸ¨ Z âŸ© â–·[ Ï† âˆ˜ ğ’’ ] âŠ¢Ë£ A â†¾ (Ï† âˆ˜ ğ’’)}
  (box : OpenBox T (ExtensionË£ Z A Ï† a) (âŸª Ïƒ âŸ« r))
  where

  module T = ExtensionLift Î± box
  module S = ExtensionLift (Î± âˆ˜á¶ Ë¢ (âŸª Ïƒ âŸ« Ã—id)) (reshapeBox Ïƒ box)

  eq : (s : âŸ¨ S âŸ©) â†’ T.filler .fill (âŸª Ïƒ âŸ« s) .out â‰¡ S.filler .fill s .out
  eq s =
    funExt Î» z â†’
    restrictExt $
    Î± .vary S T Ïƒ (_, z) r (T.pointwiseBox z) s
    âˆ™ cong (Î» b â†’ Î± .lift S ((_, z) âˆ˜ âŸª Ïƒ âŸ«) r b .fill s .out)
        (boxExt refl
          (Î» _ â†’
            diagonalCofElim (box .cof âˆ¨ Ï† z) $
            âˆ¨-elimEq (Î» _ â†’ refl) (Î» _ â†’ refl))
          refl)

opaque
  ExtensionFibStr : (Z : Shape)
    {A : Î“ â–·âŸ¨ Z âŸ© â†’ Type â„“} (Î± : FibStr A)
    (Ï† : âŸ¨ Z âŸ© â†’ Cof)
    (a : Î“ â–·âŸ¨ Z âŸ© â–·[ Ï† âˆ˜ ğ’’ ] âŠ¢Ë£ A â†¾ (Ï† âˆ˜ ğ’’))
    â†’ FibStr (ExtensionË£ Z A Ï† a)
  ExtensionFibStr Z Î± Ï† a .lift S Î³ r = ExtensionLift.filler (Î± âˆ˜á¶ Ë¢ (Î³ Ã—id))
  ExtensionFibStr Z Î± Ï† a .vary S T Ïƒ Î³ r = ExtensionVary.eq Ïƒ (Î± âˆ˜á¶ Ë¢ (Î³ Ã—id))

  --â†“ Forming extension types is stable under reindexing

  reindexExtensionFibStr : {Z : Shape}
    {A : Î“ â–·âŸ¨ Z âŸ© â†’ Type â„“} {Î± : FibStr A}
    {Ï† : âŸ¨ Z âŸ© â†’ Cof}
    {a : Î“ â–·âŸ¨ Z âŸ© â–·[ Ï† âˆ˜ ğ’’ ] âŠ¢Ë£ A â†¾ (Ï† âˆ˜ ğ’’)}
    (Ï : Î” â†’ Î“)
    â†’ ExtensionFibStr Z Î± Ï† a âˆ˜á¶ Ë¢ Ï
      â‰¡ ExtensionFibStr Z (Î± âˆ˜á¶ Ë¢ Ï Ã—id) Ï† (a âˆ˜ Ï Ã—id Ã—id)
  reindexExtensionFibStr Ï = FibStrExt Î» _ _ _ _ _ â†’ refl

Extensioná¶  : (Z : Shape) (A : Î“ â–·âŸ¨ Z âŸ© âŠ¢á¶ Type â„“) (Ï† : âŸ¨ Z âŸ© â†’ Cof)
  (a : Î“ â–·âŸ¨ Z âŸ© â–·[ Ï† âˆ˜ ğ’’ ] âŠ¢á¶  A â†¾á¶  (Ï† âˆ˜ ğ’’))
  â†’ Î“ âŠ¢á¶ Type â„“
Extensioná¶  Z A Ï† a .fst = ExtensionË£ Z (A .fst) Ï† a
Extensioná¶  Z A Ï† a .snd = ExtensionFibStr Z (A .snd) Ï† a
