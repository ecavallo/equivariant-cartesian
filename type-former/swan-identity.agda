{-

Fibration structure on Swan identity types, assuming cofibration extensionality and that
the universe of cofibrations is closed under Î£-types (i.e., is a /dominance/).

-}
module type-former.swan-identity where

open import basic
open import internal-extensional-type-theory
open import axiom
open import cofibration
open import fibration.fibration
open import fibration.trivial
open import type-former.path
open import type-former.sigma

private variable
  â„“ â„“' : Level
  Î“ Î” : Type â„“

--â†“ Definition of cofibration extensionality.

CofExtensionality : Type
CofExtensionality = âˆ€ {Ï† Ïˆ} â†’ ([ Ï† ] â†’ [ Ïˆ ]) â†’ ([ Ïˆ ] â†’ [ Ï† ]) â†’ Ï† â‰¡ Ïˆ

--â†“ Definition of closure of Cof under Î£-types.

record CofHasÎ£ : Type where
  field
    _âˆ§_ : (Ï† : Cof) â†’ ([ Ï† ] â†’ Cof) â†’ Cof
    âˆ§-pair : âˆ€ {Ï† Ïˆ} â†’ (u : [ Ï† ]) â†’ [ Ïˆ u ] â†’ [ Ï† âˆ§ Ïˆ ]
    âˆ§-fst : âˆ€ {Ï† Ïˆ} â†’ [ Ï† âˆ§ Ïˆ ] â†’ [ Ï† ]
    âˆ§-snd : âˆ€ {Ï† Ïˆ} â†’ (uv : [ Ï† âˆ§ Ïˆ ]) â†’ [ Ïˆ (âˆ§-fst uv) ]

module SwanIdentity (ext : CofExtensionality) (dom : CofHasÎ£) where

  open CofHasÎ£ dom

  private
    opaque
      âŠ¤-âˆ§-ext : âˆ€ {Ï† Ïˆ} â†’ (u : [ Ï† ]) â†’ Ï† âˆ§ Ïˆ â‰¡ Ïˆ u
      âŠ¤-âˆ§-ext {Ï†} {Ïˆ} u =
        ext
          (subst ([_] âˆ˜ Ïˆ) (cofIsStrictProp' Ï†) âˆ˜ âˆ§-snd)
          (âˆ§-pair u)

      âŠ¤-âˆ¨-ext : âˆ€ {Ï† Ïˆ} â†’ [ Ï† ] â†’ (Ï† âˆ¨ Ïˆ) â‰¡ âŠ¤
      âŠ¤-âˆ¨-ext u = ext _ (Î» _ â†’ âˆ¨l u)

      âˆ¨-âŠ¤-ext : âˆ€ {Ï† Ïˆ} â†’ [ Ïˆ ] â†’ (Ï† âˆ¨ Ïˆ) â‰¡ âŠ¤
      âˆ¨-âŠ¤-ext v = ext _ (Î» _ â†’ âˆ¨r v)

      âŠ¥-âˆ¨-ext : âˆ€ {Ï† Ïˆ} â†’ Â¬ [ Ï† ] â†’ (Ï† âˆ¨ Ïˆ) â‰¡ Ïˆ
      âŠ¥-âˆ¨-ext {Ï†} {Ïˆ} Â¬u =
        ext
          (âˆ¨-rec (ğŸ˜-rec âˆ˜ Â¬u) id (Î» _ _ â†’ cofIsStrictProp' Ïˆ))
          âˆ¨r

  Constancy : {A : Type â„“} {aâ‚€ aâ‚ : A} (p : aâ‚€ ~ aâ‚) â†’ Type â„“
  Constancy p = Î£ Ï† âˆˆ Cof , ((i : ğ•€) â†’ [ Ï† ] â†’ p .at i â‰¡ p .at 0)

  opaque
    ConstancyExt : {A : Type â„“} {aâ‚€ aâ‚ : A} (p : aâ‚€ ~ aâ‚) {câ‚€ câ‚ : Constancy p}
      â†’ câ‚€ .fst â‰¡ câ‚ .fst
      â†’ câ‚€ â‰¡ câ‚
    ConstancyExt _ eq = Î£ext eq (funExt' $ funExt' uip')

  Id : {A : Type â„“} (aâ‚€ aâ‚ : A) â†’ Type â„“
  Id aâ‚€ aâ‚ = Î£ (aâ‚€ ~ aâ‚) Constancy

  opaque
    IdExt : {A : Type â„“} {aâ‚€ aâ‚ : A} {qâ‚€ qâ‚ : Id aâ‚€ aâ‚}
      â†’ (âˆ€ i â†’ qâ‚€ .fst .at i â‰¡ qâ‚ .fst .at i)
      â†’ qâ‚€ .snd .fst â‰¡ qâ‚ .snd .fst
      â†’ qâ‚€ â‰¡ qâ‚
    IdExt {qâ‚€ = qâ‚€} {qâ‚} eqâ‚€ eqâ‚ = lemma (PathExt eqâ‚€)
      where
      lemma : qâ‚€ .fst â‰¡ qâ‚ .fst â†’ qâ‚€ â‰¡ qâ‚
      lemma refl = Î£ext refl (ConstancyExt (qâ‚€ .fst) eqâ‚)

  ConstancyË£ : {A : Î“ â†’ Type â„“} {aâ‚€ aâ‚ : Î“ âŠ¢Ë£ A} (p : Î“ âŠ¢Ë£ PathË£ A aâ‚€ aâ‚) â†’ (Î“ â†’ Type â„“)
  ConstancyË£ p Î³ = Constancy (p Î³)

  IdË£ : (A : Î“ â†’ Type â„“) (aâ‚€ aâ‚ : Î“ âŠ¢Ë£ A) â†’ (Î“ â†’ Type â„“)
  IdË£ A aâ‚€ aâ‚ Î³ = Id (aâ‚€ Î³) (aâ‚ Î³)

  opaque
    ConstancyIsTFib : {A : Î“ â†’ Type â„“} {aâ‚€ aâ‚ : Î“ âŠ¢Ë£ A} (p : Î“ âŠ¢Ë£ PathË£ A aâ‚€ aâ‚)
      â†’ TFibStr (ConstancyË£ p)
    ConstancyIsTFib p Î³ (Ï† , a) .out .fst = Ï† âˆ§ Î» u â†’ a u .fst
    ConstancyIsTFib p Î³ (Ï† , a) .out .snd i uv = a (âˆ§-fst uv) .snd i (âˆ§-snd uv)
    ConstancyIsTFib p Î³ (Ï† , a) .outâ‰¡ u = ConstancyExt (p Î³) (sym (âŠ¤-âˆ§-ext u))

  ConstancyTFib : {A : Î“ â†’ Type â„“} {aâ‚€ aâ‚ : Î“ âŠ¢Ë£ A}
    â†’ Î“ âŠ¢Ë£ PathË£ A aâ‚€ aâ‚
    â†’ Î“ âŠ¢á¶ Triv â„“
  ConstancyTFib p .fst = ConstancyË£ p
  ConstancyTFib p .snd = ConstancyIsTFib p

  opaque
    unfolding ConstancyIsTFib
    reindexConstancyTFib : {A : Î“ â†’ Type â„“} {aâ‚€ aâ‚ : Î“ âŠ¢Ë£ A}
      {p : Î“ âŠ¢Ë£ PathË£ A aâ‚€ aâ‚} (Ï : Î” â†’ Î“)
      â†’ ConstancyTFib p âˆ˜áµ—á¶  Ï â‰¡ ConstancyTFib (p âˆ˜ Ï)
    reindexConstancyTFib Ï = refl

  Idá¶  : (A : Î“ âŠ¢á¶ Type â„“) (aâ‚€ aâ‚ : Î“ âŠ¢á¶  A) â†’ Î“ âŠ¢á¶ Type â„“
  Idá¶  A aâ‚€ aâ‚ = Î£á¶  (Pathá¶  A aâ‚€ aâ‚) (TFibToFib (ConstancyTFib ğ’’))

  opaque
    reindexIdá¶  : {A : Î“ âŠ¢á¶ Type â„“} {aâ‚€ aâ‚ : Î“ âŠ¢á¶  A}
      (Ï : Î” â†’ Î“) â†’ Idá¶  A aâ‚€ aâ‚ âˆ˜á¶  Ï â‰¡ Idá¶  (A âˆ˜á¶  Ï) (aâ‚€ âˆ˜ Ï) (aâ‚ âˆ˜ Ï)
    reindexIdá¶  Ï =
      reindexÎ£á¶  Ï âˆ™
      congÎ£+ Î£á¶ 
        (reindexPathá¶  Ï)
        (reindexTFibToFib (Ï Ã—id) âˆ™ cong TFibToFib (reindexConstancyTFib (Ï Ã—id)))

  idrefl : {A : Type â„“} (a : A) â†’ Id a a
  idrefl a .fst = refl~ a
  idrefl a .snd .fst = âŠ¤
  idrefl a .snd .snd _ _ = refl

  idreflá¶  : (A : Î“ âŠ¢á¶ Type â„“) (a : Î“ âŠ¢á¶  A) â†’ Î“ âŠ¢á¶  Idá¶  A a a
  idreflá¶  A a Î³ = idrefl (a Î³)

  ----------------------------------------------------------------------------------------
  -- Singleton and singleton contractibility for identity types
  ----------------------------------------------------------------------------------------

  IdSingl : {A : Type â„“} (a : A) â†’ Type â„“
  IdSingl a = Î£ a' âˆˆ _ , Id a' a

  opaque
    IdSinglExt : {A : Type â„“} {a : A}
      {c c' : IdSingl a}
      â†’ (âˆ€ i â†’ c .snd .fst .at i â‰¡ c' .snd .fst .at i)
      â†’ c .snd .snd .fst â‰¡ c' .snd .snd .fst
      â†’ c â‰¡ c'
    IdSinglExt {c = c} {c' = c'} pathEq cofEq =
      lemma (sym (c .snd .fst .at0) âˆ™ pathEq 0 âˆ™ c' .snd .fst .at0)
      where
      lemma : c .fst â‰¡ c' .fst â†’ c â‰¡ c'
      lemma refl = Î£ext refl (IdExt pathEq cofEq)


  IdSinglá¶  : (A : Î“ âŠ¢á¶ Type â„“) (a : Î“ âŠ¢á¶  A) â†’ Î“ âŠ¢á¶ Type â„“
  IdSinglá¶  A a = Î£á¶  A (Idá¶  (A âˆ˜á¶  ğ’‘) ğ’’ (a âˆ˜ ğ’‘))

  idSinglCenterá¶  : (A : Î“ âŠ¢á¶ Type â„“) (a : Î“ âŠ¢á¶  A)
    â†’ Î“ âŠ¢á¶  IdSinglá¶  A a
  idSinglCenterá¶  A a = a ,Ë£ idreflá¶  A a

  idSinglContractá¶  : (A : Î“ âŠ¢á¶ Type â„“) (a : Î“ âŠ¢á¶  A) (c : Î“ âŠ¢á¶  IdSinglá¶  A a)
    â†’ Î“ âŠ¢á¶  Idá¶  (IdSinglá¶  A a) (idSinglCenterá¶  A a) c
  idSinglContractá¶  A a c Î³ = identity
    where
    box : (i : ğ•€) â†’ OpenBox ğ•š (cst (A $á¶  Î³)) 1
    box i .cof = âˆ‚ i âˆ¨ c Î³ .snd .snd .fst
    box i .tube j =
      âˆ¨-rec
        (âˆ‚-rec i
          (Î» _ â†’ a Î³)
          (Î» _ â†’ c Î³ .snd .fst .at j))
        (Î» _ â†’ a Î³)
        (âˆ‚-elim i
          (Î» _ v â†’ refl)
          (Î» _ v â†’
            c Î³ .snd .snd .snd j v
            âˆ™ sym (c Î³ .snd .snd .snd 1 v)
            âˆ™ c Î³ .snd .fst .at1))
    box i .cap .out = a Î³
    box i .cap .outâ‰¡ =
      âˆ¨-elimEq
        (âˆ‚-elim i (Î» _ â†’ refl) (Î» _ â†’ c Î³ .snd .fst .at1))
        (Î» _ â†’ refl)

    opaque
      square : (i : ğ•€) â†’ Filler (box i)
      square i = A .snd .lift ğ•š (cst _) 1 (box i)

    homotopy : (a Î³ , idrefl (a Î³)) ~ c Î³
    homotopy .at i .fst = square i .fill 0 .out
    homotopy .at i .snd .fst .at j = square i .fill j .out
    homotopy .at i .snd .fst .at0 = refl
    homotopy .at i .snd .fst .at1 = square i .capâ‰¡
    homotopy .at i .snd .snd .fst = (ğ•š âˆ‹ i â‰ˆ 0) âˆ¨ c Î³ .snd .snd .fst
    homotopy .at i .snd .snd .snd j =
      âˆ¨-elimEq
        (Î» iâ‰¡0 â†’
          sym (square i .fill j .outâ‰¡ (âˆ¨l (âˆ¨l iâ‰¡0)))
          âˆ™ square i .fill 0 .outâ‰¡ (âˆ¨l (âˆ¨l iâ‰¡0)))
        (Î» v â†’
          sym (square i .fill j .outâ‰¡ (âˆ¨r v))
          âˆ™ square i .fill 0 .outâ‰¡ (âˆ¨r v))
    homotopy .at0 =
      IdSinglExt
        (Î» j â†’ sym (square 0 .fill j .outâ‰¡ (âˆ¨l (âˆ¨l refl))))
        (âŠ¤-âˆ¨-ext refl)
    homotopy .at1 =
      IdSinglExt
        (Î» j â†’ sym (square 1 .fill j .outâ‰¡ (âˆ¨l (âˆ¨r refl))))
        (âŠ¥-âˆ¨-ext (0â‰ 1 âˆ˜ sym))

    identity : Id (a Î³ , idrefl (a Î³)) (c Î³)
    identity .fst = homotopy
    identity .snd .fst = c Î³ .snd .snd .fst
    identity .snd .snd i v =
      IdSinglExt
        (Î» j â†’ sym (square i .fill j .outâ‰¡ (âˆ¨r v)) âˆ™ square 0 .fill j .outâ‰¡ (âˆ¨r v))
        (âˆ¨-âŠ¤-ext v âˆ™ sym (âˆ¨-âŠ¤-ext v))

  idSinglContrRefl : (A : Î“ âŠ¢á¶ Type â„“) (a : Î“ âŠ¢á¶  A)
    â†’ idSinglContractá¶  A a (idSinglCenterá¶  A a)
      â‰¡ idreflá¶  (IdSinglá¶  A a) (idSinglCenterá¶  A a)
  idSinglContrRefl A a =
    funExt Î» Î³ â†’
    IdExt
      (Î» i â†’
        idSinglContractá¶  A a (idSinglCenterá¶  A a) Î³ .snd .snd i tt
        âˆ™ idSinglContractá¶  A a (idSinglCenterá¶  A a) Î³ .fst .at0)
      refl

  ----------------------------------------------------------------------------------------
  -- Transport along identities
  ----------------------------------------------------------------------------------------

  module _ (A : Î“ âŠ¢á¶ Type â„“) (B : Î“ â–·á¶  A âŠ¢á¶ Type â„“') {a : Î“ âŠ¢á¶  A} (b : Î“ âŠ¢á¶  B âˆ˜á¶  (id ,, a))
    where

    idSubstá¶  : {a' : Î“ âŠ¢á¶  A} (p : Î“ âŠ¢á¶  Idá¶  A a a')
      â†’ Î“ âŠ¢á¶  B âˆ˜á¶  (id ,, a')
    idSubstá¶  p Î³ =
      subst (âˆ£ B âˆ£ âˆ˜ (Î³ ,_)) (p Î³ .fst .at1)
        (B .snd .lift ğ•š _ 0 (box p Î³) .fill 1 .out)
      where
      box : {a' : Î“ âŠ¢á¶  A} (p : Î“ âŠ¢á¶  Idá¶  A a a')
        â†’ âˆ€ Î³ â†’ OpenBox ğ•š (âˆ£ B âˆ£ âˆ˜ (cst Î³ ,, p Î³ .fst .at)) 0
      box p Î³ .cof = p Î³ .snd .fst
      box p Î³ .tube i u =
        subst
          (âˆ£ B âˆ£ âˆ˜ (Î³ ,_))
          (sym (p Î³ .snd .snd i u âˆ™ p Î³ .fst .at0))
          (b Î³)
      box p Î³ .cap .out = subst (âˆ£ B âˆ£ âˆ˜ (Î³ ,_)) (sym (p Î³ .fst .at0)) (b Î³)
      box p Î³ .cap .outâ‰¡ u =
        adjustSubstEq
          (âˆ£ B âˆ£ âˆ˜ (Î³ ,_))
          refl
          refl
          (sym (p Î³ .snd .snd 0 u âˆ™ p Î³ .fst .at0))
          (sym (p Î³ .fst .at0))
          refl

    idSubstRefl : idSubstá¶  (idreflá¶  A a) â‰¡ b
    idSubstRefl =
      funExt Î» Î³ â†’
      sym (B .snd .lift ğ•š _ 0 _ .fill 1 .outâ‰¡ tt)

  ----------------------------------------------------------------------------------------
  -- Paulin-Mohring style J eliminator
  ----------------------------------------------------------------------------------------

  idJá¶  : (A : Î“ âŠ¢á¶ Type â„“) (a : Î“ âŠ¢á¶  A)
    (P : Î“ â–·á¶  IdSinglá¶  A a âŠ¢á¶ Type â„“')
    (d : Î“ âŠ¢á¶  P âˆ˜á¶  (id ,, idSinglCenterá¶  A a))
    (c : Î“ âŠ¢á¶  IdSinglá¶  A a)
    â†’ Î“ âŠ¢á¶  P âˆ˜á¶  (id ,, c)
  idJá¶  A a P d c =
    idSubstá¶  (IdSinglá¶  A a) P d (idSinglContractá¶  A a c)

  idJreflá¶  : (A : Î“ âŠ¢á¶ Type â„“) (a : Î“ âŠ¢á¶  A)
    (P : Î“ â–·á¶  IdSinglá¶  A a âŠ¢á¶ Type â„“')
    (d : Î“ âŠ¢á¶  P âˆ˜á¶  (id ,, idSinglCenterá¶  A a))
    â†’ idJá¶  A a P d (idSinglCenterá¶  A a) â‰¡ d
  idJreflá¶  A a P d =
    cong (idSubstá¶  (IdSinglá¶  A a) P d) (idSinglContrRefl A a)
    âˆ™ idSubstRefl (IdSinglá¶  A a) P d
